DEFINE_ACTION_FUNCTION npc_spells_preproc
BEGIN
  // Create Dynaheir's innate version of Horror

  LAUNCH_ACTION_FUNCTION spl_resolve_ids
    INT_VAR
    level	= 2
    STR_VAR
    ids		= "DYNAHEIR_HORROR"
    type	= "class"
    RET
    resref
  END

  spl.copy[%WIZARD_HORROR%=>%resref%]
  [
    m_type := "innate"
    m.ab.alter{s_location := "innate";;s_casting_time=1}
  ]


  // Clone Dynaheir's Witch's Glamor directly from Friends,
  // this ensures compatibility with the bard component
  // regardless of install order.

  LAUNCH_ACTION_FUNCTION spl_resolve_ids
    INT_VAR
    level	= 2
    STR_VAR
    ids		= "DYNAHEIR_WITCHS_GLAMOR"
    type	= "class"
    RET
    resref
  END

  spl.edit[%WIZARD_FRIENDS%]
  [
    m.ab_fx.clone{s_resource := "%resref%"|match=~"%s_resource%" == "%WIZARD_FRIENDS%"~}
  ]

  ACTION_IF VARIABLE_IS_SET BARD_FRIENDS
  BEGIN
    spl.edit[%BARD_FRIENDS%]
    [
      m.ab_fx.clone{s_resource := "%resref%"|match=~"%s_resource%" == "%BARD_FRIENDS%"~}
    ]
  END

  spl.copy[%WIZARD_FRIENDS%=>%resref%]
  [
    m_name := @2
    m_description := @3
    m_level = 2
    m.ab.delete{s_level < 3}
    m.ab.alter{s_level = 1|match="s_level = 3"}
    m.ab.alter{s_target := "LivingActor";;s_casting_time=2}
    m.ab_fx.alter{s_target := "PresetTarget";;s_power = 2}
  ]


  // Clone Edwin's Chant of the Red Wizards from the divine version.

  LAUNCH_ACTION_FUNCTION spl_resolve_ids
    INT_VAR
    level	= 3
    STR_VAR
    ids		= "EDWIN_CHANT"
    type	= "class"
    RET
    resref
  END

  spl.copy[%CLERIC_CHANT%d=>%resref%d %CLERIC_CHANT%e=>%resref%e]
  [
    m_level = 3
    m_type := "Wizard"
    m.ab_fx.alter{s_power = 3;;INNER_PATCH_SAVE s_resource "%s_resource%" BEGIN REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%CLERIC_CHANT%" "%resref%" END}
  ]

  spl.copy[%CLERIC_CHANT%=>%resref%]
  [
    m_level = 3
    m_type := "Wizard"
    m_casting_sound := "CAS_M03"
    m_name := @17
    m_description := @18
    m.ab_fx.alter{INNER_PATCH_SAVE s_resource "%s_resource%" BEGIN REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%CLERIC_CHANT%" "%resref%" END}
  ]


  // Clone Edwin's Assist from the divine Aid.

  LAUNCH_ACTION_FUNCTION spl_resolve_ids
    INT_VAR
    level	= 3
    STR_VAR
    ids		= "EDWIN_ASSIST"
    type	= "class"
    RET
    resref
  END

  spl.copy[%CLERIC_AID%=>%resref%]
  [
    m_level = 3
    m_type := "Wizard"
    m_casting_sound := "CAS_M03"
    m_name := @19
    m_description := @20
    //m.ab_fx.alter{INNER_PATCH_SAVE s_resource "%s_resource%" BEGIN REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%CLERIC_CHANT%" "%resref%" END}
  ]


  // Clone Yeslick's Stoneskin directly from the arcane version.

  LAUNCH_ACTION_FUNCTION spl_resolve_ids
    INT_VAR
    level	= 4
    STR_VAR
    ids		= "YESLICK_STONE_SKIN"
    type	= "class"
    RET
    resref
  END

  spl.copy[%WIZARD_STONE_SKIN%=>%resref%]
  [
    m_type := "Priest"
    m_casting_sound := "CAS_P08"
    m_description := @14
  ]
END	// npc_spells_preproc


