DEFINE_ACTION_FUNCTION epic_postproc
BEGIN
  // Create Algarth's Embattlement innates

  spl.copy[spwi420d=>%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%d]
  [
    m_name:=@28
    m_icon:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%c"
    m.ab.alter{s_icon:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%b"}
    m.ab_fx.alter{s_resource:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%"|match="s_opcode = 258"}
    m.ab_fx.alter{s_resource:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%d"|match="s_opcode = 172"}
  ]

  ACTION_IF enhanced_edition
  BEGIN
    spl.copy[spwi420d=>%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%p]
    [
      m_name:=@28
      m_icon:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%c"
      m.ab.alter{s_icon:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%b"}
      m.ab_fx.alter{s_resource:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%"|match="s_opcode = 260"}
      m.ab_fx.alter{s_resource:="%HLA_WIZARD_ALGARTHS_EMBATTLEMENT%d"|match="s_opcode = 172"}
    ]

    OUTER_SPRINT algarths_embattlement_title @28
    OUTER_SPRINT add_algarths_embattlement_label @30
    OUTER_SET sequencer_tip_strref = is_bg2 ? 60420 : 24616

    COPY_EXISTING "l_%EE_LANGUAGE%.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~uiStrings = {~
        ~uiStrings = {%WNL%%TAB%ALGARTHS_EMBATTLEMENT_TITLE = "%algarths_embattlement_title%",%WNL%%TAB%ADD_SPELLS_MATRIX_LABEL = "%add_algarths_embattlement_label%",~

    COPY_EXISTING "bgee.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~mageBookStrings = {~
        ~mageBookStrings = {%WNL%%TAB%%HLA_WIZARD_ALGARTHS_EMBATTLEMENT% = {tip = %sequencer_tip_strref%, title = 'ALGARTHS_EMBATTLEMENT_TITLE', action = "ADD_SPELLS_MATRIX_LABEL"},~
  END


  // Set correct string for Elminster's Effulgent Epuration

  spl.edit[%HLA_WIZARD_EFFULGENT_EPURATION%]
  [
    m.ab_fx.alter{s_parameter1=RESOLVE_STR_REF(@25)|match="s_opcode = 139"}
  ]
    

  // Set correct string for Interdiction

  spl.edit[mh#intda]
  [
    m.ab_fx.alter{s_parameter1=RESOLVE_STR_REF(@14)|match="s_opcode = 139"}
  ]


  // Apply CamDawg's patches to certain spells

  COPY_EXISTING "%HLA_WIZARD_CHOSEN_OF_MYSTRA%.spl" "override"
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_disease_arrays" END

  COPY_EXISTING "%HLA_CLERIC_HEALTH_BLESSING%.spl" "override"
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_poison_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_disease_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_poison_resistance_arrays" END
    BUT_ONLY_IF_IT_CHANGES


  // Append Algarth's Embattlement to contingx

  LAF add_spell_to_contingx STR_VAR ids = "HLA_WIZARD_ALGARTHS_EMBATTLEMENT" END
END


