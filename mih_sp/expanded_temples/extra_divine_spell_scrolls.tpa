DEFINE_ACTION_FUNCTION extra_divine_spell_scrolls
BEGIN
  // Change restore.itm to Lesser Restoration in BGEE
  // (is Greater Restoration in BGEE for some reason)
  
  ACTION_IF is_bg1 AND enhanced_edition AND NOT is_eet
  BEGIN
    itm.edit[restore]
    [
      m_identified_name-string = 26226
      m_identified_description-string = 26227
      m_icon := "sppr417a"
      m.ab.alter{s_icon := "sppr417a"}
      m.ab_fx.alter{s_resource := "sppr417"|match="s_opcode=146"}
    ]
  END
  

  // Create some new priest scrolls

  LAUNCH_ACTION_FUNCTION spl_make_scroll
    STR_VAR
    spell_resref	= "%CLERIC_MASS_CURE%"
    RET
    CLERIC_MASS_CURE_SCROLL	= scroll_resref
  END

  LAUNCH_ACTION_FUNCTION spl_make_scroll
    STR_VAR
    spell_resref	= "%CLERIC_HEAL%"
    RET
    CLERIC_HEAL_SCROLL	= scroll_resref
  END

  LAUNCH_ACTION_FUNCTION spl_make_scroll
    STR_VAR
    spell_resref	= "%CLERIC_RESTORATION%"
    RET
    CLERIC_RESTORATION_SCROLL	= scroll_resref
  END


  LAUNCH_ACTION_FUNCTION spl_make_scroll
    STR_VAR
    spell_resref	= "%CLERIC_RESURRECTION%"
    RET
    CLERIC_RESURRECTION_SCROLL	= scroll_resref
  END


  // Put the new scrolls in a few select temples

  ACTION_IF is_bg1
  BEGIN
    // Lady's Hall (Baldur's Gate)
    sto.edit["tem0132"]
    [
      m.item.clone{s_resref := "%CLERIC_RESTORATION_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 1|match=~"%s_resref%"=="scrlpet"~}
    ]

    // Temple of Wisdom (Friendly Arm Inn)
    sto.edit["tem2304"]
    [
      m.item.clone{s_resref := "%CLERIC_MASS_CURE_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 1|match=~"%s_resref%"=="scrlpet"~}
    ]

    // Song of the Morning (Beregost)
    sto.edit["tem3402"]
    [
      ADD_STORE_ITEM "%CLERIC_RESURRECTION_SCROLL%" AFTER "scrl63 scrlpet" #1 #0 #0 "identified" #1
    ]

    // Alvanhendar's Healing and Sundries (Gullykin)
    sto.edit["tem4003"]
    [
      ADD_STORE_ITEM "%CLERIC_HEAL_SCROLL%" AFTER "scrl63 scrlpet" #1 #0 #0 "identified" #1
    ]
  END

  ACTION_IF is_sod
  BEGIN
    // Dosia (final coalition camp)
    sto.edit["bddosia"]
    [
      m.item.clone{s_resref := "%CLERIC_MASS_CURE_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 2|match=~"%s_resref%"=="scrl63"~}
    ]

    // Crusader priest Polvi (Dragonspear basement)
    sto.edit["bdpolvi"]
    [
      m.item.clone{s_resref := "%CLERIC_RESURRECTION_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 1|match=~"%s_resref%"=="scrl61"~}
    ]

    // Onoroth's Supplies (Trollbark forest)
    sto.edit[bdonoro]
    [
      m.item.clone{s_resref := "%CLERIC_HEAL_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 1|match=~"%s_resref%"=="scrl58"~}
    ]
  END

  ACTION_IF is_bg2
  BEGIN
    // Temple of Helm (temple district)
    sto.edit["temhelm"]
    [
      m.item.clone{s_resref := "%CLERIC_HEAL_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 2|match=~"%s_resref%"=="restore"~}
    ]

    // Temple of Lathander (temple district)
    sto.edit["temlath"]
    [
      m.item.clone{s_resref := "%CLERIC_RESURRECTION_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 2|match=~"%s_resref%"=="scrl63"~}
    ]

    // Temple of Waukeen (Trademeet)
    sto.edit["temsup"]
    [
      m.item.clone{s_resref := "%CLERIC_MASS_CURE_SCROLL%";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl63"~}
    ]
  END
END	// extra_divine_spell_scrolls


