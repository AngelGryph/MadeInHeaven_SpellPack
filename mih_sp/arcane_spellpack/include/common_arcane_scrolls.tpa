DEFINE_ACTION_FUNCTION common_arcane_scrolls
BEGIN
  // Regular scrolls get added to stores with similar scrolls.

  ACTION_CLEAR_ARRAY spell_shadow
  ACTION_CLEAR_ARRAY scroll_shadow
  ACTION_CLEAR_ARRAY store_list

  LAUNCH_ACTION_FUNCTION array_read
    INT_VAR
    firstrow	= 1
    STR_VAR
    file	= "spell_shadow.2da"
    location	= "tables"
    RET_ARRAY
    spell_shadow	= array
  END

  ACTION_PHP_EACH spell_shadow AS spell=>shadow
  BEGIN
    OUTER_TEXT_SPRINT spell_scroll EVAL "%%spell%_SCROLL%"
    OUTER_TEXT_SPRINT shadow_scroll EVAL "%%shadow%_SCROLL%"

    ACTION_IF FILE_EXISTS_IN_GAME "%spell_scroll%.itm"
          AND FILE_EXISTS_IN_GAME "%shadow_scroll%.itm"
    BEGIN
      OUTER_TEXT_SPRINT $scroll_shadow("%spell_scroll%") "%shadow_scroll%"
    END
  END

  COPY_EXISTING_REGEXP - "^.*\.sto$" "nowhere"
    SET found = 0
    PHP_EACH scroll_shadow AS scroll=>shadow
    BEGIN
      PATCH_IF NOT found
           AND INDEX_BUFFER (CASE_INSENSITIVE EXACT_MATCH "%shadow%")
      BEGIN
        SET found = 1
      END
      PATCH_IF found
      BEGIN
        TEXT_SPRINT $store_list("%SOURCE_RES%") "KillroyWasHere"
      END
    END
      
  ACTION_PHP_EACH store_list AS store=>discard
  BEGIN
    sto.edit[%store%]
    [
      PHP_EACH scroll_shadow AS scroll=>shadow
      BEGIN
        m.item.clone{s_resref:="%scroll%"|match=~"%s_resref%" == "%shadow%"~}
      END
    ]
  END
END	// common_arcane_scrolls


