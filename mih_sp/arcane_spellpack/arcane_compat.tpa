DEFINE_ACTION_FUNCTION arcane_compat
BEGIN
  ACTION_IF enhanced_edition
  BEGIN
    // Alacrity
    // Barrier
    // Devastate
    // Foresight
    // Seeking
    //
    // EE has a better way of dealing with self-stacking.

    spl.edit[%WIZARD_ALACRITY% %WIZARD_BARRIER% %WIZARD_FORESIGHT%]
    [
      m.ab_fx.add{s_opcode = 321;;s_timing := "InstantPermanent";;s_target := "self";;s_resource := "%sfo_filename%"|insert_point:i=0}
      m.ab_fx.delete{s_opcode = 206}
    ]

    spl.edit[%WIZARD_DEVASTATE% %WIZARD_SEEKING%]
    [
      m.ab_fx.add{s_opcode = 321;;s_timing := "InstantPermanent";;s_target := "PresetTarget";;s_resource := "%sfo_filename%"|insert_point:i=0}
      m.ab_fx.delete{s_opcode = 206}
    ]


    // Slumber
    // Mass Charm
    //
    // Elf- and half-elf charm/sleep resistance, EE-style

    spl.edit[%WIZARD_SLUMBER% %WIZARD_MASS_CHARM%]
    [
      m.ab_fx.add{s_opcode = 324;;s_target := "PresetTarget";;s_parameter1 = 0;;s_parameter2 = entry_index = 0 ? 15 : 19;;s_probability1 = entry_index = 0 ? 89 : 29;;s_resource := "%sfo_filename%"|number:i=2 insert_point:i=0}
    ]
  END
  ELSE	// NOT enhanced_edition
  BEGIN
    // Scintillating Sphere
    // Spark Shower
    // Web of Lightning
    //
    // oBG2 did not have a save for half damage flag

    spl.edit[%WIZARD_SCINTILLATING_SPHERE% %WIZARD_SPARK_SHOWER% mh#wblit]
    [
      m.ab_fx.alter{s_dicenumber /= 2|match="s_opcode = 12"}
      m.ab_fx.clone{s_dicenumber += (p_level MODULO 2);;s_save_vs_spell = 0;;s_save_vs_breath = 0;;s_save_vs_poison = 0;;s_save_vs_wand = 0;;s_save_vs_polymorph = 0|match="s_opcode = 12"}
    ]
  END
END	// arcane_compat


