DEFINE_ACTION_FUNCTION arcane_postproc
BEGIN
  // Create Spell Matrix innates

  spl.copy[spwi420d=>%WIZARD_SPELL_MATRIX%d]
  [
    m_name:=@26
    m_icon:="%WIZARD_SPELL_MATRIX%c"
    m.ab.alter{s_icon:="%WIZARD_SPELL_MATRIX%b"}
    m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%"|match="s_opcode=258"}
    m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%d"|match="s_opcode=172"}
  ]

  ACTION_IF enhanced_edition
  BEGIN
    spl.copy[spwi420p=>%WIZARD_SPELL_MATRIX%p]
    [
      m_name:=@26
      m_icon:="%WIZARD_SPELL_MATRIX%c"
      m.ab.alter{s_icon:="%WIZARD_SPELL_MATRIX%b"}
      m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%"|match="s_opcode=260"}
      m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%p"|match="s_opcode=172"}
    ]

    OUTER_SPRINT spell_matrix_title @26
    OUTER_SPRINT add_spell_matrix_label @62
    OUTER_SET sequencer_tip_strref = is_bg2 ? 60420 : 24616

    COPY_EXISTING "l_%EE_LANGUAGE%.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~uiStrings = {~
        ~uiStrings = {%WNL%%TAB%SPELL_MATRIX_TITLE = "%spell_matrix_title%",%WNL%%TAB%ADD_SPELLS_MATRIX_LABEL = "%add_spell_matrix_label%",~

    COPY_EXISTING "bgee.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~mageBookStrings = {~
        ~mageBookStrings = {%WNL%%TAB%%WIZARD_SPELL_MATRIX% = {tip = %sequencer_tip_strref%, title = 'SPELL_MATRIX_TITLE', action = "ADD_SPELLS_MATRIX_LABEL"},~
  END


  // Add save vs. breath to Gust of Wind effects

  eff.edit[mh#gusta mh#gustb mh#gustc]
  [
    m_save_vs_breath=1
    m_resist_dispel=2
  ]
    

  // Apply CamDawg's patches to certain spells

  COPY_EXISTING "%WIZARD_SLUMBER%.spl" "override"
                "%WIZARD_MASS_CHARM%.spl" "override"
    LAUNCH_PATCH_FUNCTION cd_elven_sleep_charm_resistance END
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING "%WIZARD_SUPERHEROISM%.spl" "override"
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_fear_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_level_drain_arrays" END
    BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING "%WIZARD_MAJOR_GLOBE_OF_INVULNERABILITY%.spl" "override"
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_spell_level_1_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_spell_level_2_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_spell_level_3_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_spell_level_4_arrays" END
    LPF cd_apply_batch STR_VAR array_name = "cd_immunity_spell_level_5_arrays" END
    BUT_ONLY_IF_IT_CHANGES


  // Append some spells to contingx

  LAF add_spell_to_contingx STR_VAR ids = "WIZARD_RARYS_MNEMONIC_ENHANCER" END
  LAF add_spell_to_contingx STR_VAR ids = "WIZARD_SPELL_MATRIX" END
  LAF add_spell_to_contingx STR_VAR ids = "WIZARD_MORDENKAINENS_LUCUBRATION" END


  // Make new msectype for Alacrity

  LAUNCH_ACTION_FUNCTION resolve_sectype
    STR_VAR
    sectype	= "mhAlacrity"
    RET
    mhAlacrity	= sectype_value
  END

  spl.edit[%WIZARD_ALACRITY%]
  [
    m_sectype=%mhAlacrity%
  ]


  // Required edits on existing creatures/items/spells:
  // - Extend Magic Missile immunity to Improved Magic Missile
  //   and Major Missile.
  // - Make casting a 6th level or higher spell dispel Alacrity.

  COPY_EXISTING_REGEXP "^.+\.\(cre\|itm\|spl\)$" "override"
    LAUNCH_PATCH_FUNCTION cd_apply_batch
      STR_VAR
      array_name	= "cd_immunity_magic_missile_arrays"
    END
    
    PATCH_IF "%SOURCE_EXT%" STRING_EQUAL_CASE "spl"
         AND LONG_AT (0x0034) > 5 // spell level
    BEGIN
      LAUNCH_PATCH_FUNCTION ADD_SPELL_CFEFFECT
        INT_VAR
	opcode		= 221	// Removal: Remove Secondary
	target		= 1	// Self
	timing		= 1	// Permanent
	parameter1	= 10
	parameter2	= mhAlacrity
      END
    END

    BUT_ONLY_IF_IT_CHANGES


  // Prevent stacking of Heroism-type spells

  LAUNCH_ACTION_FUNCTION resolve_sectype
    STR_VAR
    sectype	= "mhHeroism"
    RET
    mhHeroism	= sectype_value
  END

  spl.edit[%WIZARD_HEROISM% %WIZARD_SUPERHEROISM%]
  [
    m_sectype=%mhHeroism%
    m.ab_fx.add{s_opcode=221 s_target=2 s_timing=1 s_parameter1=10 s_parameter2=%mhHeroism%|insert_point:i=0}
  ]


  // Add strings to Khelben's Dweomerdoom

  spl.edit[%WIZARD_KHELBENS_DWEOMERDOOM%]
  [
    m.ab_fx.clone{s_opcode = 139;;s_timing := "InstantPermanent";;tra_line = s_parameter1 + 80;;s_parameter1 = RESOLVE_STR_REF ( ( AT tra_line ) )|match="s_opcode = 244"}
  ]
END	// arcane_postproc


