DEFINE_ACTION_FUNCTION arcane_postproc
BEGIN
  // Create Spell Matrix innates

  spl.copy[spwi420d=>%WIZARD_SPELL_MATRIX%d]
  [
    m_name:=@26
    m_icon:="%WIZARD_SPELL_MATRIX%c"
    m.ab.alter{s_icon:="%WIZARD_SPELL_MATRIX%b"}
    m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%"|match="s_opcode=258"}
    m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%d"|match="s_opcode=172"}
  ]

  ACTION_IF enhanced_edition
  BEGIN
    spl.copy[spwi420p=>%WIZARD_SPELL_MATRIX%p]
    [
      m_name:=@26
      m_icon:="%WIZARD_SPELL_MATRIX%c"
      m.ab.alter{s_icon:="%WIZARD_SPELL_MATRIX%b"}
      m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%"|match="s_opcode=260"}
      m.ab_fx.alter{s_resource:="%WIZARD_SPELL_MATRIX%p"|match="s_opcode=172"}
    ]

    ACTION_CLEAR_ARRAY lua_lang_entries
    ACTION_DEFINE_ASSOCIATIVE_ARRAY lua_lang_entries
    BEGIN
      SPELL_MATRIX_TITLE	=> 26
      ADD_SPELL_MATRIX_LABEL	=> 62
    END
      
    LAUNCH_ACTION_FUNCTION add_to_lua_lang
      STR_VAR
      array	= "lua_lang_entries"
    END

    OUTER_SET sequencer_tip_strref = is_bg2 ? 60420 : 24616

    COPY_EXISTING "bgee.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~mageBookStrings = {~
        ~mageBookStrings = {%WNL%%TAB%%WIZARD_SPELL_MATRIX% = {tip = %sequencer_tip_strref%, title = 'SPELL_MATRIX_TITLE', action = "ADD_SPELLS_MATRIX_LABEL"},~
  END


  // Add save vs. breath to Gust of Wind effects

  eff.edit[mh#gusta mh#gustb mh#gustc]
  [
    m_save_vs_breath=1
    m_resist_dispel=2
  ]
    

  // Apply immunity effects to certain spells

  spl.edit[%WIZARD_SUPERHEROISM%]
  [
    m.immunity_effect{fear energy_drain|permanent:i=0}
  ]


  // Append some spells to contingx

  LAF add_spell_to_contingx STR_VAR ids = "WIZARD_RARYS_MNEMONIC_ENHANCER" END
  LAF add_spell_to_contingx STR_VAR ids = "WIZARD_SPELL_MATRIX" END
  LAF add_spell_to_contingx STR_VAR ids = "WIZARD_MORDENKAINENS_LUCUBRATION" END


  // Make new msectype for Alacrity

  LAUNCH_ACTION_FUNCTION resolve_sectype
    STR_VAR
    sectype	= "mhAlacrity"
    RET
    mhAlacrity	= sectype_value
  END

  spl.edit[%WIZARD_ALACRITY%]
  [
    m_sectype=%mhAlacrity%
  ]


  // Required edits on existing creatures/items/spells:
  // - Extend Magic Missile immunity to Improved Magic Missile
  //   and Major Missile.
  // - Make casting a 6th level or higher spell dispel Alacrity.

  COPY_EXISTING_REGEXP "^.+\.\(cre\|itm\|spl\)$" "override"
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      multi_match	= 1
      silent		= 1
      match_opcode	= 206
      STR_VAR
      insert		= "below"
      match_resource	= "%WIZARD_MAGIC_MISSILE%"
      resource		= "%WIZARD_SNILLOCS_MAJOR_MISSILE%"
    END

    
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      multi_match	= 1
      silent		= 1
      match_opcode	= 206
      STR_VAR
      insert		= "below"
      match_resource	= "%WIZARD_MAGIC_MISSILE%"
      resource		= "%WIZARD_IMPROVED_MAGIC_MISSILE%"
    END

    
    PATCH_IF "%SOURCE_EXT%" STRING_EQUAL_CASE "spl"
         AND LONG_AT (0x0034) > 5 // spell level
    BEGIN
      LAUNCH_PATCH_FUNCTION ADD_SPELL_CFEFFECT
        INT_VAR
	opcode		= 221	// Removal: Remove Secondary
	target		= 1	// Self
	timing		= 1	// Permanent
	parameter1	= 10
	parameter2	= mhAlacrity
      END
    END

    BUT_ONLY_IF_IT_CHANGES


  // Prevent stacking of Heroism-type spells

  LAUNCH_ACTION_FUNCTION resolve_sectype
    STR_VAR
    sectype	= "mhHeroism"
    RET
    mhHeroism	= sectype_value
  END

  spl.edit[%WIZARD_HEROISM% %WIZARD_SUPERHEROISM%]
  [
    m_sectype=%mhHeroism%
    m.ab_fx.add{s_opcode=221 s_target=2 s_timing=1 s_parameter1=10 s_parameter2=%mhHeroism%|insert_point:i=0}
  ]


  // Add strings to Khelben's Dweomerdoom

  spl.edit[%WIZARD_KHELBENS_DWEOMERDOOM%]
  [
    m.ab_fx.clone{s_opcode = 139;;s_timing := "InstantPermanent";;tra_line = s_parameter1 + 80;;s_parameter1 = RESOLVE_STR_REF ( ( AT tra_line ) )|match="s_opcode = 244"}
  ]
END	// arcane_postproc


