DEFINE_ACTION_FUNCTION restorations_postproc
BEGIN
/*
  // Create Tattoos of Power innates

  LAUNCH_ACTION_FUNCTION RES_NUM_OF_SPELL_NAME
    STR_VAR
    spell_name	= "WIZARD_TATTOOS_OF_POWER"
    RET
    spell_res
  END

  MAKE_PATCH
    icon=>"%spell_res%c"
    patch_ability_inline=>~ability_icon=>"%spell_res%b"~
    patch_effect_inline'1=>~match=>"opcode = 258" resource=>"%spell_res%"~
    patch_effect_inline'2=>~match=>"opcode = 172" resource=>"%spell_res%d"~
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "spwi420d=>%spell_res%d"
    edits	= "patch_data"
  END

  ACTION_IF enhanced_edition
  BEGIN
    MAKE_PATCH
      icon=>"%spell_res%c"
      patch_ability_inline=>~ability_icon=>"%spell_res%b"~
      patch_effect_inline'1=>~match=>"opcode = 260" resource=>"%spell_res%"~
      patch_effect_inline'2=>~match=>"opcode = 172" resource=>"%spell_res%p"~
    END

    LAUNCH_ACTION_FUNCTION clone_spell
      STR_VAR
      spell	= "spwi420p=>%spell_res%p"
      edits	= "patch_data"
    END

    ACTION_TO_UPPER spell_res

    OUTER_SPRINT tattoos_of_power_title @21
    OUTER_SPRINT add_spells_tattoos_label @23

    COPY_EXISTING "l_%EE_LANGUAGE%.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~uiStrings = {~
        ~uiStrings = {%WNL%%TAB%TATTOOS_OF_POWER_TITLE = "%tattoos_of_power_title%",%WNL%%TAB%ADD_SPELLS_TATTOOS_LABEL = "%add_spells_tattoos_label%",~

    COPY_EXISTING "bgee.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~mageBookStrings = {~
        ~mageBookStrings = {%WNL%%TAB%%spell_res% = {tip = 24616, title = 'TATTOOS_OF_POWER_TITLE', action = "ADD_SPELLS_TATTOOS_LABEL"},~
  END
*/


  // Add spell scrolls to shops

  ACTION_IF is_bg1
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "scrl6b" BEFORE "scrl79" #1 #1 #0 "identified" #3~
      literal'2=>~ADD_STORE_ITEM "scrl6c" BEFORE "scrl73" #1 #1 #0 "identified" #3~
      literal'3=>~ADD_STORE_ITEM "scrl88" AFTER "scrl87" #1 #1 #0 "identified" #3~
      literal'4=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1q" #1 #1 #0 "identified" #3~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "highhedg"
      edits	= "patch_data"
    END

    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "scrl6b" BEFORE "scrl79" #1 #1 #0 "identified" #2~
      literal'2=>~ADD_STORE_ITEM "scrl6c" BEFORE "scrl73" #1 #1 #0 "identified" #2~
      literal'3=>~ADD_STORE_ITEM "scrl88" AFTER "scrl1b" #1 #1 #0 "identified" #2~
      literal'4=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1n" #1 #1 #0 "identified" #2~
      literal'5=>~ADD_STORE_ITEM "scrl1v" AFTER "scrl1u" #1 #1 #0 "identified" #2~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "sto0703"
      edits	= "patch_data"
    END
  END

  ACTION_IF has_sod
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "scrl6b" BEFORE "scrl79" #1 #1 #0 "identified" #6~
      literal'2=>~ADD_STORE_ITEM "scrl6c" BEFORE "scrl73" #1 #1 #0 "identified" #6~
      literal'3=>~ADD_STORE_ITEM "scrl88" AFTER "scrl1b" #1 #1 #0 "identified" #6~
      literal'4=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1n" #1 #1 #0 "identified" #6~
      literal'5=>~ADD_STORE_ITEM "scrl1v" AFTER "scrla8" #1 #1 #0 "identified" #3~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "bdsorcsc"
      edits	= "patch_data"
    END

    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "scrl6b" BEFORE "scrl79" #1 #1 #0 "identified" #6~
      literal'2=>~ADD_STORE_ITEM "scrl6c" BEFORE "scrl78" #1 #1 #0 "identified" #6~
      literal'3=>~ADD_STORE_ITEM "scrl88" AFTER "scrl87" #1 #1 #0 "identified" #6~
      literal'4=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1q" #1 #1 #0 "identified" #6~
      literal'5=>~ADD_STORE_ITEM "scrl1v" AFTER "scrla8" #1 #1 #0 "identified" #6~
      literal'6=>~ADD_STORE_ITEM "scrl7n" AFTER "scrl2h" #1 #1 #0 "identified" #1~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "bdbeleg3"
      edits	= "patch_data"
    END
  END

  ACTION_IF is_bg2
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "scrl6b" BEFORE "scrl80" #1 #1 #0 "identified" #3~
      literal'2=>~ADD_STORE_ITEM "scrl6c" BEFORE "scrl78" #1 #1 #0 "identified" #3~
      literal'3=>~ADD_STORE_ITEM "scrl88" AFTER "scrl87" #1 #1 #0 "identified" #4~
      literal'4=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1q" #1 #1 #0 "identified" #4~
      literal'5=>~ADD_STORE_ITEM "scrl1v" AFTER "scrl1u" #1 #1 #0 "identified" #4~
      literal'6=>~ADD_STORE_ITEM "scrl7n" AFTER "scrlal" #1 #1 #0 "identified" #2~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "scrolls"
      edits	= "patch_data"
    END
  END

  ACTION_IF has_tob
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "scrl6b" BEFORE "scrla6" #1 #1 #0 "identified" #3~
      literal'2=>~ADD_STORE_ITEM "scrl6c" BEFORE "scrl73" #1 #1 #0 "identified" #3~
      literal'3=>~ADD_STORE_ITEM "scrl88" AFTER "scrla2" #1 #1 #0 "identified" #2~
      literal'4=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1l" #1 #1 #0 "identified" #2~
      literal'5=>~ADD_STORE_ITEM "scrl1v" AFTER "scrla8" #1 #1 #0 "identified" #2~
      literal'6=>~ADD_STORE_ITEM "scrl7n" AFTER "scrl6m" #1 #1 #0 "identified" #2~
      literal'7=>~ADD_STORE_ITEM "scrl9i" AFTER "scrl9c" #1 #1 #0 "identified" #2~
      literal'8=>~ADD_STORE_ITEM "scrl9k" AFTER "scrl9n" #1 #1 #0 "identified" #2~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "25spell"
      edits	= "patch_data"
    END
  END


  // Make Cure Blind/Deaf purchaseable at temples
  // (Can't use SFO here, no functions for this edit.)

  LAUNCH_ACTION_FUNCTION RES_NUM_OF_SPELL_NAME
    STR_VAR
    spell_name	= "CLERIC_CURE_BLIND_DEAF"
    RET
    spell_res
  END

  OUTER_SET cure_bd_desc = RESOLVE_STR_REF(@12)
  APPEND "speldesc.2da" "%spell_res%	%cure_bd_desc%"

  COPY_EXISTING_REGEXP "^.+\.sto$" "override"
    SET new_offset = 0
    SET price = 0

    GET_OFFSET_ARRAY cures_array STO_V10_CURES
    PHP_EACH cures_array AS int => cures_offset
    BEGIN
      READ_ASCII cures_offset cure_res

      PATCH_IF "%cure_res%" STRING_EQUAL_CASE "sppr212"
      BEGIN
        SET new_offset = cures_offset + 0x0c
        READ_LONG (cures_offset + 0x0008) price
      END
    END

    PATCH_IF new_offset > 0 AND price > 0
    BEGIN
      INSERT_BYTES new_offset 0x0c
      WRITE_ASCII new_offset "%spell_res%"
      WRITE_LONG (new_offset + 0x0008) price

      PATCH_IF LONG_AT (0x002c) >= new_offset
      BEGIN
        WRITE_LONG 0x002c (THIS + 0x0c)
      END

      PATCH_IF LONG_AT (0x0034) >= new_offset
      BEGIN
        WRITE_LONG 0x0034 (THIS + 0x0c)
      END

      PATCH_IF LONG_AT (0x004c) >= new_offset
      BEGIN
        WRITE_LONG 0x004c (THIS + 0x0c)
      END

      WRITE_LONG 0x0074 (THIS + 1)
    END

    BUT_ONLY_IF_IT_CHANGES
END	// restorations_postproc


