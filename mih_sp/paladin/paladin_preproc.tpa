DEFINE_ACTION_FUNCTION paladin_preproc
BEGIN
  // Create smite damage effects

  OUTER_FOR (SET i = 1; i <= 11; ++i)
  BEGIN
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#m%i%d6"
      editstring	= "opcode=>12 target=>2 timing=>1 parameter2=>0x00400000 dicenum=>%i% dicesize=>6"
    END
  END


  // Create innate version of Cure Disease

  LAUNCH_ACTION_FUNCTION add_spell_to_ids
    INT_VAR
    level		= 3
    STR_VAR
    idsname		= "PALADIN_CURE_DISEASE"
    RET
    cure_disease	= newname
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "sppr317=>%cure_disease%"
    editstring	= ~spell_type=>"innate" patch_ability_inline=>"ability_icon_loc=>4 casting_time=>1"~
  END


  // Ensure separate HLA tables for paladin kits
  // (default all but Blackguard use lupa0.2da)

  ACTION_CLEAR_ARRAY paladin_kits_hla_tables

  ACTION_DEFINE_ASSOCIATIVE_ARRAY paladin_kits_hla_tables
  BEGIN
    "cavalier"		=> "Pa1"
    "inquisitor"	=> "Pa2"
    "undead_hunter"	=> "Pa3"
  END

  ACTION_PHP_EACH paladin_kits_hla_tables AS kit => table
  BEGIN
    LAUNCH_ACTION_FUNCTION read_table_entry
      INT_VAR
      colnum	= 1
      STR_VAR
      file	= "luabbr"
      row	= "%kit%"
      RET
      value
    END

    ACTION_IF "%value%" STRING_EQUAL_CASE "Pa0"
    BEGIN
      COPY_EXISTING "luPa0.2da" "override/lu%table%.2da"

      LAUNCH_ACTION_FUNCTION write_table_entry
        INT_VAR
        colnum	= 1
        STR_VAR
	file		= "luabbr"
        row		= "%kit%"
	arguments	= "%table%"
      END
    END
  END
END	// paladin_preproc


