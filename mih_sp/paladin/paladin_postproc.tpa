DEFINE_ACTION_FUNCTION paladin_postproc
BEGIN
  // 3.5e-stype spell table

  COPY "%MOD_FOLDER%/%component_loc%/tables/mxsplpal.2da" "override"


  // Extend cavalier's bonus to dragons also to wyverns

  spl.copy[spcl221]
  [
    m.ab_fx.clone{s_resource:="mh#wyvh3"|match=~s_opcode=177 AND s_resource=="draghit"~}
    m.ab_fx.clone{s_resource:="mh#wyvd3"|match=~s_opcode=177 AND s_resource=="dragdam3"~}
  ]


  // Make Detect Evil usable at will

  spl.copy[%PALADIN_DETECT_EVIL%]
  [
    m.fx.add{s_opcode=172 s_target=1 s_timing=1 s_resource:="%PALADIN_DETECT_EVIL%"}
    m.fx.add{s_opcode=171 s_target=1 s_timing=1 s_resource:="%PALADIN_DETECT_EVIL%"}
  ]


  // Make Protection from Evil self-only and last 24 hours

  spl.copy[%PALADIN_PROTECTION_FROM_EVIL%]
  [
    m.ab.delete{s_level>1}
    m.ab.alter{s_target=5}
    m.ab_fx.alter{s_duration=(s_duration > 5) ? 1680 : s_duration}
    m.ab_fx.delete{s_opcode=9}
    m.ab.clone{s_level=12 s_projectile=158 s_icon:="sppr408b"}
  ]
END	// paladin_postproc


