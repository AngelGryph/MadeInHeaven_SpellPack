DEFINE_ACTION_FUNCTION divine_compat
BEGIN
  ACTION_IF enhanced_edition
  BEGIN
    // Protection from Cold
    // Striking
    //
    // EE has a better way of dealing with self-stacking.

    spl.edit[%CLERIC_PROTECTION_FROM_COLD% %CLERIC_STRIKING%]
    [
      m.ab_fx.add{s_opcode = 321;;s_timing := "InstantPermanent";;s_target := "PresetTarget";;s_resource := "%sfo_filename%"|insert_point:i=0}
      m.ab_fx.delete{s_opcode = 206}
    ]
  END
  ELSE	// NOT enhanced_edition
  BEGIN
    // Repel Insects
    //
    // oBG2 did not have opcode 321, use 206 instead.
    // This means Repel Insects won't stop an insect attack in progress.
    // Oh well.

    spl.edit[%CLERIC_REPEL_INSECTS%]
    [
      m.ab_fx.alter{s_opcode=206|match="s_opcode=321"}
    ]


    // Lightning Storm
    //
    // oBG2 did not have a save for half damage flag

    spl.edit[%CLERIC_LIGHTNING_STORM%]
    [
      m.adfx.alter{s_dicenumber/=2|match="opcode=12"}
      m.adfx.clone{s_dicenumber+=(p_level MODULO 2) s_save_vs_spell=0 s_save_vs_breath=0 s_save_vs_poison=0 s_save_vs_wand=0 s_save_vs_polymorph=0|match="s_opcode=12"}
    ]


    // Hallow
    // Unhallow
    //
    // The turning bonus opcode does not work on old engine,
    // turn it into a caster level bonus for clerics.

    eff.edit[mh#turn4]
    [
      m_opcode=191
      m_parameter2=1
    ]
  END


  // Compatibility with BGEE

  ACTION_IF is_bg1 AND enhanced_edition AND NOT is_bg2
  BEGIN
    // Protection from Cold
    //
    // "Protection from Cold" string is not the same between BGEE and BG2EE.

    spl.edit[%CLERIC_PROTECTION_FROM_COLD%]
    [
      m.ab_fx.alter{s_parameter1=26434|match="s_opcode = 139"}
    ]
  END
END	// divine_compat


