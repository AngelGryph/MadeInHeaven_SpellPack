DEFINE_ACTION_FUNCTION divine_compat
BEGIN
  // Compatibility with non-EE editions

  ACTION_IF NOT enhanced_edition
  BEGIN
    // Repel Insects
    //
    // oBG2 did not have opcode 321, use 206 instead.
    // This means Repel Insects won't stop an insect attack in progress.
    // Oh well.

    spl.edit[%CLERIC_REPEL_INSECTS%]
    [
      m.ab_fx.alter{s_opcode=206|match="s_opcode=321"}
    ]


    // Lightning Storm
    //
    // oBG2 did not have a save for half damage flag

    spl.edit[%CLERIC_LIGHTNING_STORM%]
    [
      m.adfx.alter{s_dicenumber/=2 save_vs_spell=0 save_vs_breath=0 save_vs_poison=0 save_vs_wand=0 save_vs_polymorph=0|match="opcode=12"}
      m.adfx.clone{s_dicenumber+=(p_level MODULO 2) save_vs_spell=0 save_vs_breath=0 save_vs_poison=0 save_vs_wand=0 save_vs_polymorph=0|match="opcode=12"}
    ]
  END


  // Compatibility with BGEE

  ACTION_IF is_bg1 AND enhanced_edition AND NOT is_bg2
  BEGIN
    // Protection from Cold
    //
    // "Protection from Cold" string is not the same between BGEE and BG2EE.

    spl.edit[%CLERIC_PROTECTION_FROM_COLD%]
    [
      m.ab_fx.alter{s_parameter1=26434|match="s_opcode = 139"}
    ]
  END
END	// divine_compat


