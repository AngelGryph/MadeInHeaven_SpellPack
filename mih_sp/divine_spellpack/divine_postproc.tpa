DEFINE_ACTION_FUNCTION divine_postproc
BEGIN
  // Further patches to Repel Insects so it can dispel an ongoing
  // Insect Swarm.

  ACTION_IF enhanced_edition
  BEGIN
    // We have EE engine, use opcode #321 to remove
    // the spells we protect against.

    MAKE_PATCH
      clone_effect_inline=>~match=>"opcode = 206" clone_at_top=>1 opcode=>321~
    END
  END
  ELSE
  BEGIN
    // Old engine, give insect spells a special sectype and
    // use opcodes 221 to remove spells by sectype.
    // (Much clumsier, and much less reliable.)

    LAUNCH_ACTION_FUNCTION add_silent_sectype
      STR_VAR
      sectype	= "mh#InsectSwarm"
      RET
      sectype_value
    END

    LAUNCH_ACTION_FUNCTION edit_spell
      STR_VAR
      spell	= "CLERIC_SUMMON_INSECTS CLERIC_INSECT_PLAGUE CLERIC_CREEPING_DOOM BLACK_DRAGON_INSECT"
      editstring	= "secondary=>%sectype_value%"
    END

    MAKE_PATCH
      clone_effect_inline=>~match=>"opcode = 83 and parameter2 = 227" number_to_add=>2 opcode=>"entry_index from [205 221]" parameter1=>0 parameter2=>%sectype_value%~
    END
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_REPEL_INSECTS"
    edits	= "patch_data"
  END


  // Hallow/Unhallow's turning bonus does not work on old engine,
  // turn it into a caster level bonus for clerics

  ACTION_IF NOT enhanced_edition
  BEGIN
    MAKE_PATCH
      opcode=>191
      parameter2=>1
    END

    LAUNCH_ACTION_FUNCTION edit_effect
      STR_VAR
      effect	= "mh#turn4"
      edits	= "patch_data"
    END
  END


  // "Protection from Cold" string is not the same between BGEE and BG2EE.

  OUTER_SET prot_cold_strref = RESOLVE_STR_REF(@39)

  MAKE_PATCH
    patch_effect_inline=>~match=>"opcode = 139" parameter1=>%prot_cold_strref%~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_PROTECTION_FROM_COLD"
    edits	= "patch_data"
  END


  // Unhide some previously hidden spells

  LAUNCH_ACTION_FUNCTION edit_hidespl
    INT_VAR
    is_hidden	= 0
    STR_VAR
    spell	= "%CLERIC_FAERIE_FIRE%"
  END


  // Make Heroism/Superheroism mutually exclusive

  MAKE_PATCH
    clone_effect_inline=>~match=>"opcode = 206" resource=>"object_index from [%CLERIC_SUPERHEROISM% %CLERIC_HEROISM%]" only_once=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "%CLERIC_HEROISM% %CLERIC_SUPERHEROISM%"
    edits	= "patch_data"
  END
END	// divine_postproc


