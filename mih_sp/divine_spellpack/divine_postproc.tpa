DEFINE_ACTION_FUNCTION divine_postproc
BEGIN
  // Create Exalted Triad innates

  spl.copy[spwi420d=>%CLERIC_EXALTED_TRIAD%d]
  [
    m_name:=@50
    m_icon:="%CLERIC_EXALTED_TRIAD%c"
    m.ab.alter{s_icon:="%CLERIC_EXALTED_TRIAD%b"}
    m.ab_fx.alter{s_resource:="%CLERIC_EXALTED_TRIAD%"|match="s_opcode=258"}
    m.ab_fx.alter{s_resource:="%CLERIC_EXALTED_TRIAD%d"|match="s_opcode=172"}
  ]

  ACTION_IF enhanced_edition
  BEGIN
    spl.copy[spwi420p=>%CLERIC_EXALTED_TRIAD%p]
    [
      m_name:=@50
      m_icon:="%CLERIC_EXALTED_TRIAD%c"
      m.ab.alter{s_icon:="%CLERIC_EXALTED_TRIAD%b"}
      m.ab_fx.alter{s_resource:="%CLERIC_EXALTED_TRIAD%"|match="s_opcode=260"}
      m.ab_fx.alter{s_resource:="%CLERIC_EXALTED_TRIAD%p"|match="s_opcode=172"}
    ]

    ACTION_CLEAR_ARRAY lua_lang_entries
    ACTION_DEFINE_ASSOCIATIVE_ARRAY lua_lang_entries
    BEGIN
      EXALTED_TRIAD_TITLE	=> 50
      ADD_EXALTED_TRIAD_LABEL	=> 52
    END
      
    LAUNCH_ACTION_FUNCTION add_to_lua_lang
      STR_VAR
      array	= "lua_lang_entries"
    END

    OUTER_SET sequencer_tip_strref = is_bg2 ? 60420 : 24616

    COPY_EXISTING "bgee.lua" "override"
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
        ~mageBookStrings = {~
        ~mageBookStrings = {%WNL%%TAB%%CLERIC_EXALTED_TRIAD% = {tip = %sequencer_tip_strref%, title = 'EXALTED_TRIAD_TITLE', action = "ADD_SPELLS_TRIAD_LABEL"},~
  END


  // Apply immunity effects to certain spells

/* Doesn't work for non-permanent effects.
  spl.edit[%CLERIC_REPEL_INSECTS%]
  [
    m.immunity_effect{insects|permanent:i=0}
  ]
*/

  spl.edit[%CLERIC_SUPERHEROISM%]
  [
    m.immunity_effect{fear energy_drain|permanent:i=0}
  ]

  spl.edit[%CLERIC_CELESTIAL_PROTECTION%]
  [
    m.immunity_effect{poison diseased|permanent:i=0}
  ]

  spl.edit[%CLERIC_MASS_NEGATIVE_PLANE_PROTECTION%]
  [
    m.immunity_effect{energy_drain|permanent:i=0}
  ]


  // Append some spells to contingx

  LAF add_spell_to_contingx STR_VAR ids = "CLERIC_EXALTED_TRIAD" END


  // Prevent stacking of Heroism-type spells

  LAUNCH_ACTION_FUNCTION resolve_sectype
    STR_VAR
    sectype	= "mhHeroism"
    RET
    mhHeroism	= sectype_value
  END

  spl.edit[%CLERIC_HEROISM% %CLERIC_SUPERHEROISM%]
  [
    m_sectype=%mhHeroism%
    m.ab_fx.add{s_opcode=221 s_target=2 s_timing=1 s_parameter1=10 s_parameter2=%mhHeroism%|insert_point:i=0}
  ]
END	// divine_postproc


