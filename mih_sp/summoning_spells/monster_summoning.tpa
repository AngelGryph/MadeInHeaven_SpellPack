DEFINE_ACTION_FUNCTION monster_summoning
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "monster_summoning_base_power"
    RET
    monster_summoning_base_power	= value
  END


  ACTION_CLEAR_ARRAY spell_scrolls

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_scrolls
  BEGIN
    WIZARD_MONSTER_SUMMONING_0	=> mh#scms0
    WIZARD_MONSTER_SUMMONING_1	=> scrl1l
    WIZARD_MONSTER_SUMMONING_2	=> scrl2a
    WIZARD_MONSTER_SUMMONING_3	=> scrl2g
    WIZARD_MONSTER_SUMMONING_4	=> scrl7n
    WIZARD_MONSTER_SUMMONING_5	=> mh#scmd5
    WIZARD_MONSTER_SUMMONING_6	=> mh#scmd6
    WIZARD_MONSTER_SUMMONING_7	=> mh#scmd7
  END


  OUTER_FOR (SET i = 0; i < 8; ++i)
  BEGIN
    MAKE_PATCH
      opcode=>127
      timing=>1
      parameter1=>"%i% * 16 + %monster_summoning_base_power%"
      parameter2=>"-1"
      resource=>"monsum0%i%"
      vvc=>"spmonsum"
    END

    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect	= "spmon%i%"
      edits	= "patch_data"
    END

    MAKE_PATCH
      spell_type=>"wizard"
      level=>"%i% + 2"
      say_name=>"1000 + %i%"
      say_description=>"2000 + %i%"
      icon=>"monsum%i%c"
      casting_sound=>"CAS_M03"
      casting_animation=>14
      school=>"conjuration"
      secondary=>0x06
      unusable_diviner=>1
      hostile=>1
      add_ability_inline=>~ability_type=>1 ability_icon_loc=>2 ability_icon=>"monsum%i%b" ability_target=>4 ability_range=>20 ability_min_level=>1 projectile=>1 casting_time=>"%i% + 2"~
      add_effect_inline=>~opcode=>177 target=>1 duration=>60 power=>"%i% + 2" parameter2=>2 resource=>"spmon%i%"~
    END

    LAUNCH_ACTION_FUNCTION make_spell
      INT_VAR
      level	= i + 2
      type	= 2
      STR_VAR
      idsname	= "WIZARD_MONSTER_SUMMONING_%i%"
      edits	= patch_data
      RET
      resref	= newname
    END

    LAUNCH_ACTION_FUNCTION make_scroll_of_spell
      STR_VAR
      resref
      scrollname	= $spell_scrolls("WIZARD_MONSTER_SUMMONING_%i%")
    END
  END
END	// monster_summoning


