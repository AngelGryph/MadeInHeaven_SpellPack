DEFINE_ACTION_FUNCTION animal_summoning
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "animal_summoning_base_power"
    RET
    animal_summoning_base_power	= value
  END

  OUTER_FOR (SET i = 1; i < 4; ++i)
  BEGIN
    MAKE_PATCH
      opcode=>127
      timing=>1
      parameter1=>"%i% * 16 + %animal_summoning_base_power%"
      parameter2=>"-1"
      resource=>"anisum0%i%"
      vvc=>"spanisum"
    END

    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect	= "spanim%i%"
      edits	= "patch_data"
    END

    MAKE_PATCH
      spell_type=>"cleric"
      level=>"%i% + 3"
      say_name=>"3000 + %i%"
      say_description=>"4000 + %i%"
      icon=>"anisum%i%c"
      casting_sound=>"CAS_P03"
      casting_animation=>14
      school=>"conjuration"
      secondary=>0x06
      //priest_type=>"druid"
      hostile=>1
      add_ability_inline=>~ability_type=>1 ability_icon_loc=>2 ability_icon=>"anisum%i%b" ability_target=>4 ability_range=>20 ability_min_level=>1 projectile=>1 casting_time=>"%i% + 6"~
      add_effect_inline=>~opcode=>177 target=>1 duration=>60 power=>"%i% + 3" parameter2=>2 resource=>"spanim%i%"~
    END

    LAUNCH_ACTION_FUNCTION make_spell
      INT_VAR
      level	= i + 3
      type	= 1
      STR_VAR
      idsname	= "CLERIC_ANIMAL_SUMMONING_%i%"
      edits	= patch_data
    END
  END
END	// animal_summoning


