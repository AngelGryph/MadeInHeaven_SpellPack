DEFINE_ACTION_FUNCTION augment_summoning
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "monster_summoning_base_power"
    RET
    monster_summoning_base_power	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "monster_summoning_level_rate"
    RET
    monster_summoning_level_rate	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "animal_summoning_base_power"
    RET
    animal_summoning_base_power	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "animal_summoning_level_rate"
    RET
    animal_summoning_level_rate	= value
  END


  OUTER_FOR (SET i = 1; i <= 7; ++i)
  BEGIN
    MAKE_PATCH
      opcode=>127
      target=>1
      timing=>1
      parameter1=>"(%i% - 1) * %monster_summoning_level_rate% + %monster_summoning_base_power%"
      parameter2=>"-1"
      resource=>"monsum0%i%"
    END  

    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect	= "spmon%i%"
      edits	= "patch_data"
    END
  END


  OUTER_FOR (SET i = 1; i <= 3; ++i)
  BEGIN
    MAKE_PATCH
      opcode=>127
      target=>1
      timing=>1
      parameter1=>"(%i% - 1) * %animal_summoning_level_rate% + %animal_summoning_base_power%"
      parameter2=>"-1"
      resource=>"anisum0%i%"
    END  

    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect	= "spanim%i%"
      edits	= "patch_data"
    END
  END


  MAKE_PATCH
    delete_effect=>null
    add_effect_inline=>~opcode=>177 target=>1 duration=>"(level + ability_true_level - 1) * 6" parameter2=>2 resource=>"object_index from [spmon1 spmon2 spmon3 spmon4 spmon5 spmon6 spmon7 spanim1 spanim2 spanim3]"~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    INT_VAR
    allow_missing	= 1
    STR_VAR
    spell	= "WIZARD_MONSTER_SUMMONING_1 WIZARD_MONSTER_SUMMONING_2 WIZARD_MONSTER_SUMMONING_3 WIZARD_MONSTER_SUMMONING_4 WIZARD_MONSTER_SUMMONING_5 WIZARD_MONSTER_SUMMONING_6 WIZARD_MONSTER_SUMMONING_7 CLERIC_ANIMAL_SUMMONING_1 CLERIC_ANIMAL_SUMMONING_2 CLERIC_ANIMAL_SUMMONING_3"
    edits	= "patch_data"
  END
END	// augment_summoning


