DEFINE_ACTION_FUNCTION expanded_temple_services
BEGIN
  // Change restore.itm to Lesser Restoration in BGEE
  // (is Greater Restoration in BGEE for some reason)
  
  ACTION_IF is_bg1 AND enhanced_edition AND NOT is_eet
  BEGIN
    itm.edit[restore]
    [
      m_identified_name:=#26226
      m_identified_description:=#26227
      m_icon:="sppr417a"
      m.ab.alter{s_icon:="sppr417a"}
      m.ab_fx.alter{s_resource:="sppr417"|match="s_opcode=146"}
    ]
  END
  

  // Add more cures

  ACTION_CLEAR_ARRAY cures_list
  ACTION_CLEAR_ARRAY temples_list

  OUTER_SET string = RESOLVE_STR_REF(@0)
  APPEND "speldesc.2da" "sppr317	%string%" UNLESS "sppr317"
  OUTER_SPRINT $cures_list("sppr303") "sppr317"

  OUTER_SET string = RESOLVE_STR_REF(@1)
  APPEND "speldesc.2da" "sppr417	%string%" UNLESS "sppr417"
  OUTER_SPRINT $cures_list("sppr307") "sppr417"

  ACTION_IF VARIABLE_IS_SET CLERIC_CURE_BLIND_DEAF
        AND FILE_EXISTS_IN_GAME "%CLERIC_CURE_BLIND_DEAF%.spl"
  BEGIN
    OUTER_SET string = RESOLVE_STR_REF(@2)
    APPEND "speldesc.2da" "%CLERIC_CURE_BLIND_DEAF%	%string%"
    OUTER_SPRINT $cures_list("sppr212") "%CLERIC_CURE_BLIND_DEAF%"
  END

  ACTION_IF VARIABLE_IS_SET CLERIC_BREAK_ENCHANTMENT
        AND FILE_EXISTS_IN_GAME "%CLERIC_BREAK_ENCHANTMENT%.spl"
  BEGIN
    OUTER_SET string = RESOLVE_STR_REF(@3)
    APPEND "speldesc.2da" "%CLERIC_BREAK_ENCHANTMENT%	%string%"
    OUTER_SPRINT $cures_list("sppr504") "%CLERIC_BREAK_ENCHANTMENT%"
  END

  COPY_EXISTING_REGEXP - "^.+\.sto$" "nowhere"
    LPF struct_get STR_VAR arguments="type" RET type=value END
    PATCH_IF type = 3
    BEGIN
      SPRINT $temple_list("%SOURCE_RES%") "god=here"
    END

  ACTION_PHP_EACH temple_list AS temple=>discard
  BEGIN
    sto.edit[%temple%]
    [
      PHP_EACH cures_list AS old=>new
      BEGIN
        PATCH_IF INDEX_BUFFER(CASE_INSENSITIVE EXACT_MATCH "%new%") == "-1"
	BEGIN
          m.cure.clone{s_resref:="%new%"|match=~"%s_resref%" == "%old%"~}
	END
      END
    ]
  END
END	// expanded_temple_services


