DEFINE_ACTION_FUNCTION spell_improvements
BEGIN
  // ===============================
  // Protection from Normal Missiles
  // ===============================

  spl.edit[%WIZARD_PROTECTION_FROM_NORMAL_MISSILES% spra303]
  [
    m.ab_fx.clone{s_opcode=89 s_parameter1=0 s_parameter2=50|match="s_opcode = 142"}
  ]


  // ===========
  // Wyvern Call
  // ===========

  cre.copy[wyvernsu=>mh#wyvsu]
  [
    m_ac="-5"
    m_str_base=25
    m_resist_magic=35
    m_resist_missile=80
    m_level1=11
    m_hp=94
    m_thac0=9
    m_morale_break=4
    m_morale_recovery=30
    m_saves:="4,6,5,6,5"
  ]


  eff.copy[wyvernsu=>mh#wyvsu]
  [
    m_resource:="mh#wyvsu"
  ]


  spl.edit[%WIZARD_WYVERN_CALL%]
  [
    m_description:=@1
    m.ab.delete{s_level > 1}
    m.ab.clone{s_level=(13 + entry_index)|number:i=17}
    m.ab_fx.alter{s_duration=(p_level = 1 ? 72 : p_level * 6) s_resource:="mh#wyvsu"|match="s_opcode = 177"}
    m.ab_fx.clone{|match="s_opcode = 177 AND p_level > 17"}
    m.ab_fx.clone{|match="s_opcode = 177 AND p_level > 23"}
    m.ab_fx.clone{|match="s_opcode = 177 AND p_level > 29"}
  ]


  itm.edit[scrl7w]
  [
    m_identified_description:=@1
  ]


  // ==========
  // Shillelagh
  // ==========

  itm.edit[shille]
  [
    m_magical=1
    m_enchantment=1
    m_proficiency=115
    m.fx.add{s_opcode=233 s_target=1 s_timing=2 s_parameter1=1 s_parameter2=115}
    m.ab.alter{s_attack_bonus=1 s_damage_bonus=1}
  ]


  spl.edit[%CLERIC_SHILLELAGH%]
  [
    m_description:=@2
  ]


  // ========
  // Barkskin
  // ========

  spl.edit[%CLERIC_BARKSKIN%]
  [
    m_description:=@7
    m.ab_fx.alter{s_parameter1=(6 - s_parameter1) s_parameter2=0|match="s_opcode = 0"}
    m.ab_fx.clone{s_parameter1=6 s_parameter2=1|match="s_opcode = 0"}
  ]


  // ===========
  // Flame Blade
  // ===========

  OUTER_FOR (SET flame = 1; flame <= 10; ++flame)
  BEGIN
    itm.copy[fblade=>mh#flm%flame%]
    [
      m_magical=1
      m_enchantment=6
      m_silver=1
      m_cold_iron=1
      m.fx.add{s_opcode=233 s_target=1 s_timing=2 s_parameter1=1 s_parameter2=0x5f}
      m.ab.alter{s_attack_bonus=4 s_dicesize=0 s_dicenumber=0 s_damage_bonus=0 s_damage_type=0 s_add_strength_bonus=0}
      m.ab_fx.alter{s_parameter1=%flame% s_dicenumber=1 s_dicesize=8|match="s_opcode = 12"}
    ]
  END

  spl.edit[%CLERIC_FLAME_BLADE%]
  [
    m_description:=@8
    m.ab_fx.alter{s_resource:="mh#flm1"|match="s_opcode = 111 AND p_level < 4"}
    m.ab_fx.alter{s_resource:="mh#flm2"|match="s_opcode = 111 AND (p_level / 2) = 2"}
    m.ab_fx.alter{s_resource:="mh#flm3"|match="s_opcode = 111 AND (p_level / 2) = 3"}
    m.ab_fx.alter{s_resource:="mh#flm4"|match="s_opcode = 111 AND (p_level / 2) = 4"}
    m.ab_fx.alter{s_resource:="mh#flm5"|match="s_opcode = 111 AND (p_level / 2) = 5"}
    m.ab_fx.alter{s_resource:="mh#flm6"|match="s_opcode = 111 AND (p_level / 2) = 6"}
    m.ab_fx.alter{s_resource:="mh#flm7"|match="s_opcode = 111 AND (p_level / 2) = 7"}
    m.ab_fx.alter{s_resource:="mh#flm8"|match="s_opcode = 111 AND (p_level / 2) = 8"}
    m.ab_fx.alter{s_resource:="mh#flm9"|match="s_opcode = 111 AND (p_level / 2) = 9"}
    m.ab_fx.alter{s_resource:="mh#flm10"|match="s_opcode = 111 AND (p_level / 2) > 9"}
  ]
END	// spell_improvements


