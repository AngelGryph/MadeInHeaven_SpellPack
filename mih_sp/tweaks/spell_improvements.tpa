DEFINE_ACTION_FUNCTION spell_improvements
BEGIN
  // =====================
  // Larloch's Minor Drain
  // =====================

  MAKE_PATCH
    level_based_blocks=>"if this=10 then -1 else this+1"
    patch_effect_inline'1=>~match=>"opcode = 12" duration=>60 parameter1=>ability_true_level dicenum=>1 dicesize=>4 mode=>BIT3~
    patch_effect_inline'2=>~match=>"opcode = 18" duration=>60 parameter1=>ability_true_level dicenum=>1 dicesize=>4~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spwi119a spin104a"
    edits	= "patch_data"
  END


  // ==========
  // Shillelagh
  // ==========

  MAKE_PATCH
    magical=>1
    enchantment=>1
    proficiency=>PROFICIENCYCLUB
    add_effect_global_inline=>"opcode=>233 target=>1 timing=>2 parameter1=>1 parameter2=>115"
    patch_ability_inline=>"to_hit=>1 damage_bonus=>1"
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "shille"
    edits	= "patch_data"
  END


  // ========
  // Barkskin
  // ========

  MAKE_PATCH
    patch_effect_inline=>~match=>"opcode = 0" parameter1=>"6 - parameter1" parameter2=>0~
    clone_effect_inline=>~match=>"opcode = 0" parameter1=>6 parameter2=>1~
    say_description=>7
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_BARKSKIN"
    edits	= "patch_data"
  END


  // ===========
  // Flame Blade
  // ===========

  OUTER_FOR (SET i = 1; i <= 10; ++i)
  BEGIN
    MAKE_PATCH
      magical=>1
      enchantment=>6
      silver=>1
      cold_iron=>1
      add_effect_global_inline=>~opcode=>233 target=>1 timing=>2 parameter1=>1 parameter2=>0x5f~
      patch_ability_inline=>~to_hit=>4 ability_dicesize=>0 ability_numdice=>0 damage_bonus=>0 damage_type=>0 strength_bonus=>0~
      patch_effect_inline=>~match=>opcode=12 parameter1=>%i% dicenum=>1 dicesize=>8~
    END

    LAUNCH_ACTION_FUNCTION clone_item
      STR_VAR
      item	= "fblade=>mh#flm%i%"
      edits	= "patch_data"
    END
  END

  MAKE_PATCH
    patch_effect_inline'1=>~match=>"opcode=111 and ability_min_level<20" resource=>"ability_min_level from [mh#flm1 mh#flm1 mh#flm1 mh#flm2 mh#flm2 mh#flm3 mh#flm3 mh#flm4 mh#flm4 mh#flm5 mh#flm5 mh#flm6 mh#flm6 mh#flm7 mh#flm7 mh#flm8 mh#flm8 mh#flm9 mh#flm9]"~
    patch_effect_inline'2=>~match=>"opcode=111 and ability_min_level>19" resource=>"mh#flm10"~
    say_description=>8
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_FLAME_BLADE"
    edits	= "patch_data"
  END
END	// spell_improvements



