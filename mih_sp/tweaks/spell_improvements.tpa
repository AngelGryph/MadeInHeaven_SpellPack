DEFINE_ACTION_FUNCTION spell_improvements
BEGIN
  // ===============================
  // Protection from Normal Missiles
  // ===============================

  MAKE_PATCH
    clone_effect_inline=>~match=>"opcode = 142" opcode=>89 parameter1=>0 parameter2=>50~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "WIZARD_PROTECTION_FROM_NORMAL_MISSILES spra303"
    edits	= "patch_data"
  END


  // ===========
  // Wyvern Call
  // ===========

  MAKE_PATCH
    ac=>0
    strength=>20
    dexterity=>18
    constitution=>17
    resist_magic=>35
    level=>11
    enforce_hp=>at_worst
    enforce_thac0=>at_worst
    enforce_saves=>at_worst
  END

  LAUNCH_ACTION_FUNCTION clone_creature
    STR_VAR
    creature	= "wyvernsu=>mh#wyvsu"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION clone_effect
    STR_VAR
    effect	= "wyvernsu=>mh#wyvsu"
    editstring	= ~resource=>"mh#wyvsu"~
  END

  MAKE_PATCH
    level_based_blocks=>"if this = 30 then -1 else this + 1"
    patch_effect_inline=>~duration=>"ability_min_level * 6" resource=>"mh#wyvsu"~
    clone_effect_inline=>~match=>"ability_min_level > 17" number_to_add=>"(ability_min_level - 12) / 6"~
    say_description=>1
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "WIZARD_WYVERN_CALL"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "scrl7w"
    editstring	= "say_description=>1"
  END


  // ==========
  // Shillelagh
  // ==========

  MAKE_PATCH
    magical=>1
    enchantment=>1
    proficiency=>PROFICIENCYCLUB
    add_effect_global_inline=>"opcode=>233 target=>1 timing=>2 parameter1=>1 parameter2=>115"
    patch_ability_inline=>"to_hit=>1 damage_bonus=>1"
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "shille"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_SHILLELAGH"
    editstring	= "say_description=>2"
  END


  // ========
  // Barkskin
  // ========

  MAKE_PATCH
    patch_effect_inline=>~match=>"opcode = 0" parameter1=>"6 - parameter1" parameter2=>0~
    clone_effect_inline=>~match=>"opcode = 0" parameter1=>6 parameter2=>1~
    say_description=>7
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_BARKSKIN"
    edits	= "patch_data"
  END


  // ===========
  // Flame Blade
  // ===========

  OUTER_FOR (SET i = 1; i <= 10; ++i)
  BEGIN
    MAKE_PATCH
      magical=>1
      enchantment=>6
      silver=>1
      cold_iron=>1
      add_effect_global_inline=>~opcode=>233 target=>1 timing=>2 parameter1=>1 parameter2=>0x5f~
      patch_ability_inline=>~to_hit=>4 ability_dicesize=>0 ability_numdice=>0 damage_bonus=>0 damage_type=>0 strength_bonus=>0~
      patch_effect_inline=>~match=>opcode=12 parameter1=>%i% dicenum=>1 dicesize=>8~
    END

    LAUNCH_ACTION_FUNCTION clone_item
      STR_VAR
      item	= "fblade=>mh#flm%i%"
      edits	= "patch_data"
    END
  END

  MAKE_PATCH
    patch_effect_inline'1=>~match=>"opcode=111 and ability_min_level<20" resource=>"ability_min_level from [mh#flm1 mh#flm1 mh#flm1 mh#flm2 mh#flm2 mh#flm3 mh#flm3 mh#flm4 mh#flm4 mh#flm5 mh#flm5 mh#flm6 mh#flm6 mh#flm7 mh#flm7 mh#flm8 mh#flm8 mh#flm9 mh#flm9]"~
    patch_effect_inline'2=>~match=>"opcode=111 and ability_min_level>19" resource=>"mh#flm10"~
    say_description=>8
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_FLAME_BLADE"
    edits	= "patch_data"
  END


  // =============
  // Bolt of Glory
  // =============

  MAKE_PATCH
    target=>2
    dicenum=>10
    dicesize=>"object_index from [10 6 4 8]"
  END

  LAUNCH_ACTION_FUNCTION edit_effect
    STR_VAR
    effect	= "boltdem boltelem boltprim boltund"
    edits	= "patch_data"
  END

  MAKE_PATCH
    casting_time=>4
    say_description=>9
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "CLERIC_BOLT_OF_GLORY"
    edits	= "patch_data"
  END
END	// spell_improvements


