DEFINE_ACTION_FUNCTION pfe_blocks_domination
BEGIN
  // Leaving this as WeiDU code for now.
  // Not sure how to do this in SFO.

  COPY_EXISTING_REGEXP - ".*\.spl" "dummy"
    GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
    PHP_EACH ab_array AS int => ab_offset
    BEGIN
      GET_OFFSET_ARRAY2 fx_array ab_offset SPL_V10_HEAD_EFFECTS
      PHP_EACH fx_array AS int => fx_offset
      BEGIN
        READ_SHORT (fx_offset + 0x0000) opcode
        READ_LONG (fx_offset + 0x0008) parameter2

        PATCH_IF opcode == 142 AND parameter2 == 43
        BEGIN
          SPRINT $domination_list("%SOURCE_RES%") "-"
        END
      END
    END


  COPY_EXISTING_REGEXP ".*\.spl" "override"
    SET protection_from_evil = 0

    GET_OFFSET_ARRAY fx_array SPL_V10_GEN_EFFECTS
    PHP_EACH fx_array AS int => fx_offset
    BEGIN
      READ_SHORT (fx_offset + 0x0000) opcode
      READ_LONG (fx_offset + 0x0008) parameter2

      PATCH_IF opcode == 142 AND parameter2 == 9
      BEGIN
        SET protection_from_evil = 1
      END
    END

    GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
    PHP_EACH ab_array AS int => ab_offset
    BEGIN
      GET_OFFSET_ARRAY2 fx_array ab_offset SPL_V10_HEAD_EFFECTS
      PHP_EACH fx_array AS int => fx_offset
      BEGIN
        READ_SHORT (fx_offset + 0x0000) opcode
        READ_LONG (fx_offset + 0x0008) parameter2

        PATCH_IF opcode == 142 AND parameter2 == 9
        BEGIN
          SET protection_from_evil = 1
        END
      END
    END
        
    PATCH_IF (protection_from_evil)
    BEGIN
      PHP_EACH domination_list AS domination_res => blah
      BEGIN
        LAUNCH_PATCH_FUNCTION CLONE_EFFECT
          INT_VAR
          match_opcode		= 142	// Graphics: Display Special Effect Icon
          match_parameter2	= 9	// Protection from Evil
          opcode		= 206	// Spell: Protection from Spell
          parameter1		= RESOLVE_STR_REF(@0)
          parameter2		= 0
          STR_VAR
          resource		= "%domination_res%"
        END
      END
    END

    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING_REGEXP ".*\.itm" "override"
    SET protection_from_evil = 0

    GET_OFFSET_ARRAY fx_array ITM_V10_GEN_EFFECTS
    PHP_EACH fx_array AS int => fx_offset
    BEGIN
      READ_SHORT (fx_offset + 0x0000) opcode
      READ_LONG (fx_offset + 0x0008) parameter2

      PATCH_IF opcode == 142 AND parameter2 == 9
      BEGIN
        SET protection_from_evil = 1
      END
    END

    GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
    PHP_EACH ab_array AS int => ab_offset
    BEGIN
      GET_OFFSET_ARRAY2 fx_array ab_offset ITM_V10_HEAD_EFFECTS
      PHP_EACH fx_array AS int => fx_offset
      BEGIN
        READ_SHORT (fx_offset + 0x0000) opcode
        READ_LONG (fx_offset + 0x0008) parameter2

        PATCH_IF opcode == 142 AND parameter2 == 9
        BEGIN
          SET protection_from_evil = 1
        END
      END
    END
        
    PATCH_IF (protection_from_evil)
    BEGIN
      PHP_EACH domination_list AS domination_res => blah
      BEGIN
        LAUNCH_PATCH_FUNCTION CLONE_EFFECT
          INT_VAR
          match_opcode		= 142	// Graphics: Display Special Effect Icon
          match_parameter2	= 9	// Protection from Evil
          opcode		= 206	// Spell: Protection from Spell
          parameter1		= RESOLVE_STR_REF(@9)
          parameter2		= 0
          STR_VAR
          resource		= "%domination_res%"
        END
      END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// pfe_blocks_domination


