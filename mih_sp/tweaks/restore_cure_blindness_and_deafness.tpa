DEFINE_ACTION_FUNCTION restore_cure_blindness_and_deafness
BEGIN
  // Create sppr316 (Cure Blind/Deaf) by cloning sppr317 (Cure Disease)

  OUTER_SET blindness_cured = RESOLVE_STR_REF(@6)

  MAKE_PATCH
    say_name=>2
    say_description=>3
    icon=>"sppr316c"
    patch_ability_inline=>"ability_icon=>sppr316b"
    patch_effect_inline=>"match=>opcode=139 parameter1=>%blindness_cured%"
    delete_opcodes=>"77 79 321"
    delete_effect=>"opcode=240 and parameter2 is_in [7 48]"
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "sppr317=>sppr316"
    edits	= "patch_data"
  END


  // Make Cure Disease no longer cure blindness/deafness

  MAKE_PATCH
    say_description=>4
    delete_opcodes=>"75 81"
    delete_effect=>"opcode=240 and parameter2 is_in [8 112]"
    add_effect_inline=>"opcode=>17 target=>2 timing=>1 parameter1=>5 power=>3 resist_dispel=>3"
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    INT_VAR
    allow_missing	= 1
    STR_VAR
    spell	= "sppr317 k0pa001 rr#palcd"
    edits	= "patch_data"
  END


  // Make Cure Blind/Deaf purchaseable at temples
  // (Can't use SFO here, no functions for this edit.)

  OUTER_SET cure_bd_desc = RESOLVE_STR_REF(@5)
  APPEND "speldesc.2da" "sppr316	%cure_bd_desc%"

  COPY_EXISTING_REGEXP "^.+\.sto$" "override"
    SET new_offset = 0
    GET_OFFSET_ARRAY cures_array STO_V10_CURES
    PHP_EACH cures_array AS int => cures_offset
    BEGIN
      READ_ASCII cures_offset cure_res

      PATCH_IF "%cure_res%" STRING_EQUAL_CASE "sppr303"
      BEGIN
        SET new_offset = cures_offset + 0x0c
        READ_LONG (cures_offset + 0x0008) new_price
      END
    END

    PATCH_IF new_offset > 0
    BEGIN
      INSERT_BYTES new_offset 0x0c
      WRITE_ASCII new_offset "sppr316"
      WRITE_LONG (new_offset + 0x0008) new_price

      PATCH_IF LONG_AT (0x002c) >= new_offset
      BEGIN
        WRITE_LONG 0x002c (THIS + 0x0c)
      END

      PATCH_IF LONG_AT (0x0034) >= new_offset
      BEGIN
        WRITE_LONG 0x0034 (THIS + 0x0c)
      END

      PATCH_IF LONG_AT (0x004c) >= new_offset
      BEGIN
        WRITE_LONG 0x004c (THIS + 0x0c)
      END

      WRITE_LONG 0x0074 (THIS + 1)
    END

    BUT_ONLY_IF_IT_CHANGES


  // Fix allied SoD priest.

  ACTION_IF has_sod
  BEGIN
    COPY_EXISTING "bdff1000.cre" "override"
      REMOVE_MEMORIZED_SPELL "sppr317"
      ADD_MEMORIZED_SPELL "sppr316" #2 "priest"
      ADD_MEMORIZED_SPELL "sppr317" #2 "priest"
      

    COPY_EXISTING "bdshffis.bcs" "override"
      DECOMPILE_AND_PATCH
      BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
         ~StateCheck(.*,STATE_BLIND)~ ~False()~
        REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
         ~CheckSpellState(.*,DEAFENED)~ ~False()~

        APPEND_FILE TEXT "%mod_root%/%component_loc%/scripts/use_cure_blind_deaf.baf"
      END
  END
END	// restore_cure_blindness_and_deafness


