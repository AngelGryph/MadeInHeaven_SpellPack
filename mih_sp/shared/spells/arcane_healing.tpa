DEFINE_ACTION_FUNCTION arcane_healing
BEGIN
  LAUNCH_ACTION_FUNCTION check_label
    STR_VAR
    label	= "mh#arcane_healing"
    RET
    installed	= value
  END

  ACTION_IF NOT installed
  BEGIN
    LAUNCH_ACTION_FUNCTION make_label
      STR_VAR
      label	= "mh#arcane_healing"
    END

    ACTION_FOR_EACH idsname IN CLERIC_CURE_LIGHT_WOUNDS CLERIC_SLOW_POISON CLERIC_CURE_DISEASE CLERIC_CURE_SERIOUS_WOUNDS CLERIC_CURE_CRITICAL_WOUNDS CLERIC_NEUTRALIZE_POISON CLERIC_MASS_CURE
    BEGIN
      OUTER_TEXT_SPRINT oldname EVAL "%%idsname%%"

      OUTER_PATCH_SAVE new_ids "%idsname%"
      BEGIN
        REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "CLERIC_" "ARCANE_"
      END

      COPY_EXISTING - "%oldname%.spl" "%workspace%"
        LAUNCH_PATCH_FUNCTION SPL_read_level
	  RET
	  level		= value
	END

      LAUNCH_ACTION_FUNCTION add_spell_to_ids
        INT_VAR
	level
	type		= 4
        STR_VAR
        idsname		= "%new_ids%"
	RET
	newname
      END

      LAUNCH_ACTION_FUNCTION clone_spell
        STR_VAR
	spell		= "%oldname%=>%newname%"
	editstring	= ~spell_type=>"wizard" priest_type=>0 casting_sound=>"CAS_M07"~
      END
    END
  END
END	// arcane_healing


