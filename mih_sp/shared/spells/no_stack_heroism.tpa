DEFINE_ACTION_FUNCTION no_stack_heroism
BEGIN
  OUTER_TEXT_SPRINT spell_list ~~

  ACTION_FOR_EACH spell IN WIZARD_HEROISM WIZARD_SUPERHEROISM CLERIC_HEROISM CLERIC_SUPERHEROISM
  BEGIN
    ACTION_IF VARIABLE_IS_SET "%spell%"
    BEGIN
      OUTER_TEXT_SPRINT spell_list EVAL "%spell_list% %%spell%%"
    END
  END

  ACTION_IF enhanced_edition
  BEGIN
    MAKE_PATCH
      var_spell_list=>"[%spell_list%]"
      delete_effect=>"opcode is_in [206 321]"
      add_effect_inline=>~number_to_add=>"length spell_list" insert_point=>0 opcode=>321 target=>2 power=>level resource=>"entry_index from spell_list"~
    END
  END
  ELSE
  BEGIN
    MAKE_PATCH
      var_spell_list=>"[%spell_list%]"
      delete_effect=>"opcode is_in [206 321]"
      clone_effect_inline=>~number_to_add=>"length spell_list" clone_at_bottom=>1 only_once=>1 match=>"opcode = 142" opcode=>206 parameter2=>0 resource=>"entry_index from spell_list"~
    END
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "%spell_list%"
    edits	= "patch_data"
  END
END	// no_stack_heroism


