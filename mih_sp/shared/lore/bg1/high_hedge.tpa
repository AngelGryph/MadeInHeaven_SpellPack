DEFINE_ACTION_FUNCTION high_hedge
BEGIN
  // High Hedge
  //
  //
  // Described in Volo's Guide to the Sword Coast.
  //
  // Thalantyr is both canocially and in-game a conjurer, yet his shop
  // lacks the appropriate spell scrolls.  I add a bunch of scrolls from
  // the conjuration school.
  //
  // Canonically, Thalantyr possesses a Staff of Power, but such an item is
  // overpowered for BG1 so I add it and his Robe of the Good Archmagi only
  // visually; he won't drop them if killed.
  //
  // Thalantyr is neutral good, so it's unlikely he goes around digging
  // up grave sites to construct flesh golems.  Volo reports the tower is
  // protected by some sort of invisible creature, so I change them to
  // invisible stalkers.


  // Appropriate scrolls in shop

  sto.edit[highhedg]
  [
    ADD_STORE_ITEM "scrl95" AFTER "scrl94" #1 #0 #0 "identified" #3
    ADD_STORE_ITEM "scrla3" AFTER "scrla2 scrl1q" #1 #0 #0 "identified" #3
    ADD_STORE_ITEM "scrl6e" AFTER "scrla2 scrl1q" #1 #0 #0 "identified" #3
    ADD_STORE_ITEM "scrl1l" AFTER "scrl1i" #1 #0 #0 "identified" #3
    ADD_STORE_ITEM "scrl1t" AFTER "scrl1q" #1 #0 #0 "identified" #3
    ADD_STORE_ITEM "scrl2a" BEFORE "wand08" #1 #0 #0 "identified" #1
    ADD_STORE_ITEM "scrl6r" BEFORE "wand08" #1 #0 #0 "identified" #1
  ]


  // Cosmetic items on Thalantyr

  LAUNCH_ACTION_FUNCTION install
    INT_VAR
    overwrite	= 0
    STR_VAR
    file	= "staf12.itm"
    location	= "items"
    locbase
  END

  cre.edit[thalan]
  [
    m.cre_add_items{staf12(weapon1,unstealable,undropable) clck15(armor,unstealable,undropable)|replace:i=1}
  ]


  // Replace Flesh Golems with Invisible Stalkers

  cre.copy[%tutu_scriptbg%stalke=>fgolem]
  [
    m_script_override:="none"
    m_script_class:="fgolem"
    m_script_race:="shout"
    m_script_general:="none"
    m_script_default:="wdasight"
  ]
END	// high_hedge


