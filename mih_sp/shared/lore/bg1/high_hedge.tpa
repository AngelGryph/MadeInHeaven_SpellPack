DEFINE_ACTION_FUNCTION high_hedge
BEGIN
  // High Hedge
  //
  //
  // Described in Volo's Guide to the Sword Coast.
  //
  // Thalantyr is both canocially and in-game a conjurer, yet his shop
  // lacks the appropriate spell scrolls.  I add a bunch of scrolls from
  // the conjuration school.
  //
  // Canonically, Thalantyr possesses a Staff of Power, but such an item is
  // overpowered for BG1 so I add it and his Robe of the Good Archmagi only
  // visually; he won't drop them if killed.
  //
  // Thalantyr is neutral good, so it's unlikely he goes around digging
  // up grave sites to construct flesh golems.  Volo reports the tower is
  // protected by some sort of invisible creature, so I change them to
  // invisible stalkers.


  // Appropriate scrolls in shop

  sto.edit[highhedg]
  [
    PATCH_IF NOT enhanced_edition
    BEGIN
      // Add missing scrolls so we have the same base.
      m.item.clone{s_resref := "scrla2";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl98"~}
      m.item.clone{s_resref := "scrlai";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrla2"~}
      m.item.clone{s_resref := "scrl6e";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrlai"~}
      m.item.clone{s_resref := "scrla6";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl6e"~}
      m.item.clone{s_resref := "scrl6l";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl1q"~}
      m.item.clone{s_resref := "scrl6i";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl6l"~}
      m.item.clone{s_resref := "scrl6h";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl6i"~}
      m.item.clone{s_resref := "scrl6g";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl6h"~}
    END
    m.item.clone{s_resref := "scrl95";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl94"~}
    m.item.clone{s_resref := "scrla3";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrla2"~}
    m.item.clone{s_resref := "scrl6e";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrla3"~}
    m.item.clone{s_resref := "scrl1l";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl1i"~}
    m.item.clone{s_resref := "scrl1t";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 3|match=~"%s_resref%"=="scrl1q"~}
    m.item.clone{s_resref := "scrl2a";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 1|match=~"%s_resref%"=="scrl6g"~}
    m.item.clone{s_resref := "scrl6r";;s_charges_1 = 1;;s_charges_2 = 0;;s_charges_3 = 0;;s_identified = 1;;s_number_in_stock = 1|match=~"%s_resref%"=="scrl2a"~}
  ]


  // Cosmetic items on Thalantyr

  LAUNCH_ACTION_FUNCTION install
    INT_VAR
    overwrite	= 0
    STR_VAR
    file	= "staf12.itm"
    location	= "items"
    locbase
  END

  cre.edit[thalan]
  [
    m.cre_add_items{staf12(weapon1,unstealable,undropable) clck15(armor,unstealable,undropable)|replace:i=1}
  ]


  // Replace Flesh Golems with Invisible Stalkers

  cre.copy[stalke=>fgolem]
  [
    m_script_override:="none"
    m_script_class:="fgolem"
    m_script_race:="shout"
    m_script_general:="none"
    m_script_default:="wdasight"
  ]
END	// high_hedge


