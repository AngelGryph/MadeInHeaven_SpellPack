DEFINE_ACTION_FUNCTION nashkel
BEGIN
  // Nashkel
  //
  //
  // Described in Lands of Intrigue book 2: Amn.
  //
  // The Nashkel Inn is called The Northern Light and is run by a 5th level
  // wizard named Ries Kensidar.  I make it so and add some first- and second
  // level scrolls to his shop.
  //
  // The Nashkel shop is called Cloud Peak Outfitters and is run by a 4th
  // level hafling cleric called Salla Pitontoes.  I made it so.
  //
  // Berrun Ghastkill is a human ranger, not an elf.  I make it so.


  // Nashkel Inn: The Northern Light

  sto.edit[inn4801]
  [
    m_name:=@0
    m_can_buy=1
    m_can_sell=1
    m_can_identify=1
    m_lore=100

    m.itempurchased.delete{}
    PATCH_FOR_EACH itemcat IN 1 6 9 10 11 16 26 32 34 35
    BEGIN
      m.itempurchased.add{s_category=%itemcat%}
    END

    PATCH_FOR_EACH item IN scrl1b scrl1c scrl85 scrl86 scrl87 scrl89 scrl90 scrl91 scrl92 scrl93 scrl94 scrl95 scrl96 scrl97 scrl98 scrl99
    BEGIN
      m.item.add{s_resref:=%item% s_charges_1=1 s_charges_2=1 s_number_in_stock=1 s_identified=1}
    END

    PATCH_FOR_EACH item IN scrl66 scrl67 scrl68 scrl69 scrl70 scrl71 scrl72 scrl73 scrl75 scrl76 scrl77 scrl78 scrl79 scrl80 scrl81 scrl82 scrl83 scrl84
    BEGIN
      m.item.add{s_resref:=%item% s_charges_1=1 s_charges_2=1 s_number_in_stock=2 s_identified=1}
    END
  ]


  cre.edit[innkn2]
  [
    m_both_names:=@1
    m_race:=human
    m_class:=mage
    m_gender:=male
    m_animation:=mage_male_human
    m_level1=5
    m_hp=15
    m_thac0=19
    m_abils:="8,16,12,10,10,14"
    m_saves:="14,11,13,15,12"
    m_alignment:=lawful_neutral
    m_script_class:=mage3
    m_script_default:=wtasight
    m.cre_add_items{staf01(weapon1) brac01(gloves)|replace:i=1}
    m.cre_add_spells{WIZARD_MAGIC_MISSILE(3) WIZARD_STINKING_CLOUD WIZARD_AGANNAZAR_SCORCHER WIZARD_GHOST_ARMOR}
    m.cre_set_proficiencies{QUARTERSTAFF=>1}
  ]


  // Nashkel Store: Cloud Peaks Outfitters

  sto.edit[sto4803]
  [
    say_name:=@2
  ]


  cre.edit[shopkn]
  [
    m_both_names:=@3
    m_race:=halfling
    m_class:=cleric
    m_gender:=female
    m_animation:=cleric_female_halfling
    m_level1=4
    m_hp=24
    m_thac0=18
    m_abils:="8,12,16,14,10,10"
    m_saves:="9,13,12,15,14"
    m_alignment:=chaotic_good
    m_script_class:=priest1
    m_script_race:=none
    m_script_general:=shopkn
    m_script_default:=wtasight
    m.cre_add_items{blun04(weapon1) shld01(shield) chan01(armor)|replace:i=1}
    m.cre_add_spells{CLERIC_COMMAND(2) CLERIC_ENTANGLE CLERIC_HOLD_PERSON(2)}
    m.cre_set_proficiencies{MACE=>1 SWORDANDSHIELD=>1}
  ]

  LAF copy_soundset STR_VAR src="halffg" dst="shopkn" END


  // Berrun Ghastkill

  cre.edit[berrun]
  [
    m_race:=human
    m_animation:=fighter_male_human
  ]


  // Area Edits

  are.edit[%Nashkel%]
  [
    // The Northern Light
    m.region.alter{s_info:=@0|match=~"%s_name%" == "Info4801"~}
    m.mapnote.alter{s_text:=@0|match=~s_x = 1330 AND s_y = 652~}

    // Cloudpeak Outfitters
    m.region.alter{s_info:=@2|match=~"%s_name%" == "Info4803"~}
    m.mapnote.alter{s_text:=@2|match=~s_x = 1655 AND s_y = 867~}
  ]
END	// nashkel


