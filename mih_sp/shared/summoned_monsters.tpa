DEFINE_ACTION_FUNCTION summoned_monsters
BEGIN
  // snakegsu - Giant Snake

  ACTION_IF NOT FILE_EXISTS_IN_GAME "p1-3p.itm"
  BEGIN
    MAKE_PATCH
      add_effect_inline'1=>~match=>parent_type=1 opcode=>25 target=>2 duration=>24 parameter1=>2 parameter2=>2~
      add_effect_inline'2=>~match=>parent_type=1 opcode=>142 target=>2 duration=>24 parameter2=>6~
    END

    LAUNCH_ACTION_FUNCTION clone_item
      STR_VAR
      item	= "p1-3=>p1-3p"
      edits	= "patch_data"
    END
  END


  ACTION_IF NOT FILE_EXISTS_IN_GAME "snakegsu.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      INT_VAR
      allow_missing	= 1
      STR_VAR
      creature		= "bdsnak01=>snakegsu snakeg01=>snakegsu"
      editstring	= "xp_value=>0 gold=>0 allegiance=>neutral gender=>summoned thac0=>16 replace_items=>p1-3p(weapon1)"
    END
  END


  // sumelwat - Lesser Water Elemental

  ACTION_IF NOT FILE_EXISTS_IN_GAME "sumelwat.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "sumelfir=>sumelwat"
      editstring	= "say_name=>0 say_unidentified_name=>0 animation=>water_weird resist_fire=>-10 resist_magic_fire=>0 resist_cold=>10 class=>elemental_water replace_items=>b1-8m2(weapon1)"
    END
  END


  // jellmusu - Mustard Jelly

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "jellmusu"
    editstring	= "xp_value=>0 gold=>0 allegiance=>neutral gender=>summoned"
  END


  // Priestly Water Elementals

  ACTION_IF NOT FILE_EXISTS_IN_GAME "elwatpr.cre"
         OR NOT FILE_EXISTS_IN_GAME "elwatpr2.cre"
         OR NOT FILE_EXISTS_IN_GAME "elwatpr3.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "elwatg01=>elwatpr elwatg01=>elwatpr2 elwatg01=>elwatpr3"
      editstring	= ~no_corpse=>1 xp_value=>0 allegiance=>controlled gender=>summoned level1=>"object_index from [12 16 24]" hitpoints=>"object_index from [96 128 192]" thac0=>"object_index from [9 5 0]" strength=>"object_index from [18 19 22]" say_both_names=>1~
    END

    LAUNCH_ACTION_FUNCTION clone_effect
      STR_VAR
      effect		= "speart1p=>spwat1p speart2p=>spwat2p speart3p=>spwat3p"
      editstring	= ~resource=>"object_index from [elwatpr elwatpr2 elwatpr3]"~
    END
  END


  // Priestly Air Elementals

  ACTION_IF NOT FILE_EXISTS_IN_GAME "elairpr.cre"
         OR NOT FILE_EXISTS_IN_GAME "elairpr2.cre"
         OR NOT FILE_EXISTS_IN_GAME "elairpr3.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "elair01=>elairpr elairg01=>elairpr2"
      editstring	= "no_corpse=>1 xp_value=>0 allegiance=>controlled gender=>summoned"
    END

    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "elairpr2=>elairpr3"
      editstring	= "level1=>24 hitpoints=>192 thac0=>0 strength=>22"
    END

    LAUNCH_ACTION_FUNCTION clone_effect
      STR_VAR
      effect		= "spair1=>spair1p spair2=>spair2p spair3=>spair3p"
      editstring	= ~resource=>"object_index from [elairpr elairpr2 elairpr3]"~
    END
  END


  // Batch-create missing summoning effects

  ACTION_DEFINE_ASSOCIATIVE_ARRAY summon_effect
  BEGIN
    "bearcasu"	=> "spanisum"
    "bearbrsu"	=> "spanisum"
    "bearposu"	=> "spanisum"
    "catjagsu"	=> "spanisum"
    "catliosu"	=> "spanisum"
    "jellmusu"	=> "trbloba"
    "ogresu"	=> "spmonsum"
    "sumelwat"	=> "spplanar"
    "snakegsu"	=> "spanisum"
    "spidphsu"	=> "spmonsum"
    "spidswsu"	=> "spmonsum"
    "wolfdisu"	=> "spanisum"
    "wolfwisu"	=> "spanisum"
  END

  ACTION_PHP_EACH summon_effect AS summon => effect
  BEGIN
    ACTION_IF NOT FILE_EXISTS_IN_GAME "%summon%.eff"
    BEGIN
      CREATE EFF "%summon%"
        WRITE_LONG	0x0010 67	// Summon: Creature Summoning
        WRITE_LONG	0x001c 5
        WRITE_SHORT	0x0024 1
        WRITE_LONG	0x002c 100
        WRITE_ASCIIE	0x0030 "%summon%" #8
        WRITE_ASCII	0x0070 "%effect%" #8
    END
  END
END	// summoned_monsters


