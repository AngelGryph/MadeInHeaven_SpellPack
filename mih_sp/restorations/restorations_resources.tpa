DEFINE_ACTION_FUNCTION install_rename_icons
  STR_VAR
  oldname	= ""
  newname	= ""
BEGIN
  ACTION_FOR_EACH suffix IN "a" "b" "c"
  BEGIN
    ACTION_IF NOT FILE_EXISTS_IN_GAME "%newname%%suffix%.bam"
    BEGIN
      COPY_LARGE "%MOD_FOLDER%/%component_loc%/graphics/%oldname%%suffix%.bam" "override/%newname%%suffix%.bam"
    END
  END
END


DEFINE_ACTION_FUNCTION restorations_resources
BEGIN
  // Try to claim the original idsnames if they haven't been reused yet

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "claim_original_idsnames"
    RET
    claim_original_idsnames	= value
  END

  ACTION_IF claim_original_idsnames
  BEGIN
    ACTION_CLEAR_ARRAY original_idsnames
    ACTION_DEFINE_ASSOCIATIVE_ARRAY original_idsnames
    BEGIN
      "2204" => "WIZARD_FOG_CLOUD"
      "2315" => "WIZARD_WRAITH_FORM"
      "2610" => "WIZARD_MONSTER_SUMMONING_4"
      "2901" => "WIZARD_SPELL_INVULNERABILITY"

      "1114" => "CLERIC_ANIMAL_FRIENDSHIP"
      "1115" => "CLERIC_ENDURE_HEAT_COLD"
    END

    ACTION_PHP_EACH original_idsnames AS spell_num => spell_name
    BEGIN
      APPEND "spell.ids" "%spell_num% %spell_name%" UNLESS "^%spell_num%"
    END

    CLEAR_IDS_MAP
  END
END	// restorations_resources


