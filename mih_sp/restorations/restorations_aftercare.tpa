DEFINE_ACTION_FUNCTION restorations_aftercare
BEGIN
  // Create spell scrolls for restored wizard spells

  ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_scroll
  BEGIN
    "mh#rs204"	=>	"WIZARD_FOG_CLOUD"
    "scrl1r"	=>	"WIZARD_WRAITH_FORM"
    "scrl1v"	=>	"WIZARD_DIMENSION_DOOR"
    "mh#rs610"	=>	"WIZARD_MONSTER_SUMMONING_4"
    "mh#rs901"	=>	"WIZARD_SPELL_INVULNERABILITY"
  END

  ACTION_PHP_EACH spell_scroll AS scrollname => spell_name
  BEGIN
    LAUNCH_ACTION_FUNCTION RES_NUM_OF_SPELL_NAME
      STR_VAR
      spell_name
      RET
      spell_res
      spell_num
    END

    LAUNCH_ACTION_FUNCTION make_scroll_of_spell
      STR_VAR
      resref		= "%spell_res%"
      scrollname
    END
  END


  // Add spell scrolls to shops

  ACTION_IF is_bg1
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "mh#rs204" AFTER "scrl87" #1 #1 #0 "identified" #3~
      literal'2=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1q" #1 #1 #0 "identified" #3~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "highhedg"
      edits	= "patch_data"
    END

    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "mh#rs204" AFTER "scrl1b" #1 #1 #0 "identified" #2~
      literal'2=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1n" #1 #1 #0 "identified" #2~
      literal'3=>~ADD_STORE_ITEM "scrl1v" AFTER "scrl1u" #1 #1 #0 "identified" #2~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "sto0703"
      edits	= "patch_data"
    END
  END

  ACTION_IF has_sod
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "mh#rs204" AFTER "scrl1b" #1 #1 #0 "identified" #6~
      literal'2=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1n" #1 #1 #0 "identified" #6~
      literal'3=>~ADD_STORE_ITEM "scrl1v" AFTER "scrla8" #1 #1 #0 "identified" #3~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "bdsorc"
      edits	= "patch_data"
    END

    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "mh#rs204" AFTER "scrl87" #1 #1 #0 "identified" #6~
      literal'2=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1q" #1 #1 #0 "identified" #6~
      literal'3=>~ADD_STORE_ITEM "scrl1v" AFTER "scrla8" #1 #1 #0 "identified" #6~
      literal'4=>~ADD_STORE_ITEM "mh#rs610" AFTER "scrl2h" #1 #1 #0 "identified" #1~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "bdbeleg3"
      edits	= "patch_data"
    END
  END

  ACTION_IF is_bg2
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "mh#rs204" AFTER "scrl87" #1 #1 #0 "identified" #4~
      literal'2=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1q" #1 #1 #0 "identified" #4~
      literal'3=>~ADD_STORE_ITEM "scrl1v" AFTER "scrl1u" #1 #1 #0 "identified" #4~
      literal'4=>~ADD_STORE_ITEM "mh#rs610" AFTER "scrlal" #1 #1 #0 "identified" #2~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "scrolls"
      edits	= "patch_data"
    END
  END

  ACTION_IF has_tob
  BEGIN
    MAKE_PATCH
      literal'1=>~ADD_STORE_ITEM "mh#rs204" AFTER "scrla2" #1 #1 #0 "identified" #2~
      literal'2=>~ADD_STORE_ITEM "scrl1r" AFTER "scrl1l" #1 #1 #0 "identified" #2~
      literal'3=>~ADD_STORE_ITEM "scrl1v" AFTER "scrla8" #1 #1 #0 "identified" #2~
      literal'4=>~ADD_STORE_ITEM "mh#rs610" AFTER "scrl6m" #1 #1 #0 "identified" #2~
      literal'4=>~ADD_STORE_ITEM "mh#rs901" AFTER "scrl9n" #1 #1 #0 "identified" #2~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "25spell"
      edits	= "patch_data"
    END
  END


  // Make Cure Blind/Deaf purchaseable at temples
  // (Can't use SFO here, no functions for this edit.)

  OUTER_SET cure_bd_desc = RESOLVE_STR_REF(@12)
  APPEND "speldesc.2da" "sppr316	%cure_bd_desc%"

  COPY_EXISTING_REGEXP "^.+\.sto$" "override"
    SET new_offset = 0
    SET price = 0

    GET_OFFSET_ARRAY cures_array STO_V10_CURES
    PHP_EACH cures_array AS int => cures_offset
    BEGIN
      READ_ASCII cures_offset cure_res

      PATCH_IF "%cure_res%" STRING_EQUAL_CASE "sppr212"
      BEGIN
        SET new_offset = cures_offset + 0x0c
        READ_LONG (cures_offset + 0x0008) price
      END
    END

    PATCH_IF new_offset > 0 AND price > 0
    BEGIN
      INSERT_BYTES new_offset 0x0c
      WRITE_ASCII new_offset "sppr316"
      WRITE_LONG (new_offset + 0x0008) price

      PATCH_IF LONG_AT (0x002c) >= new_offset
      BEGIN
        WRITE_LONG 0x002c (THIS + 0x0c)
      END

      PATCH_IF LONG_AT (0x0034) >= new_offset
      BEGIN
        WRITE_LONG 0x0034 (THIS + 0x0c)
      END

      PATCH_IF LONG_AT (0x004c) >= new_offset
      BEGIN
        WRITE_LONG 0x004c (THIS + 0x0c)
      END

      WRITE_LONG 0x0074 (THIS + 1)
    END

    BUT_ONLY_IF_IT_CHANGES
END	// restorations_aftercare


