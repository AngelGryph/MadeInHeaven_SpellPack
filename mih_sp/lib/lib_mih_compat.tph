// locate_mod_tra_dir
//
// This function takes a mod name and tries to locate the correct
// directory for its translation files.  Useful for making cross-mod
// content that use STATE_WHICH_SAYS or similar to append to other
// mod's dialog files.

DEFINE_ACTION_FUNCTION locate_mod_tra_dir
  STR_VAR
  mod_name	= "dummy"
  RET
  tra_dir
  tra_dir_ext
  tra_dir_int
BEGIN
  ACTION_FOR_EACH dir IN "weidu_external/%modname%/language" "weidu_external/modname/tra" "weidu_external/lang/%mod_name%"
  BEGIN
    ACTION_IF DIRECTORY_EXISTS "%dir%"
    BEGIN
      OUTER_TEXT_SPRINT tra_dir_ext "%dir%"
    END
  END

  ACTION_FOR_EACH dir IN "%modname%/language" "%mod_name%/lang" "%mod_name%/tra"
  BEGIN
    ACTION_IF DIRECTORY_EXISTS "%dir%"
    BEGIN
      OUTER_TEXT_SPRINT tra_dir_int "%dir%"
    END
  END

  ACTION_IF NOT VARIABLE_IS_SET tra_dir_int
  BEGIN
    WARN ~locate_mod_tra_dir: No translation files found for mod "mod_name"!~
  END

  ACTION_IF VARIABLE_IS_SET tra_dir_ext
  BEGIN
    OUTER_TEXT_SPRINT tra_dir "%tra_dir_ext%"
  END
  ELSE
  BEGIN
    OUTER_TEXT_SPRINT tra_dir "%tra_dir_int%"
  END
END


// erase_journal_entries_on_bg2_transition
//
// If called on any game that is not BGT (including EET), this function does
// nothing at all.
//
// On BGT, this function takes a dialog file (minus the .dlg extention),
// decompiles it and extracts the string references of all JOURNAL,
// SOLVED_JOURNAL and // UNSOLVED_JOURNAL entries, then edits an area
// script of Chateau Irenicus to erase all these strings from the journal.

DEFINE_ACTION_FUNCTION erase_journal_entries_on_bg2_transition
  STR_VAR
  dialog    = "dummy"
BEGIN
  ACTION_IF GAME_IS "bgt" AND FILE_EXISTS_IN_GAME "aram00.bcs"
  BEGIN
    COPY_EXISTING - "%dialog%.dlg" "%mod_name%/work"
      GET_OFFSET_ARRAY trans_array 0x0014 4 0x0010 4 0 0 0x0020
      PHP_EACH trans_array AS int => trans_offset
      BEGIN
        PATCH_IF (LONG_AT (trans_offset) & BIT4)
    BEGIN
      READ_SLONG (trans_offset + 0x0008) strref
      PATCH_IF strref > 0
      BEGIN
        DEFINE_ASSOCIATIVE_ARRAY journal_entries
        BEGIN
          "%strref%" => "1"
        END
      END
    END
      END

    COPY_EXISTING "aram00.bcs" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      PHP_EACH journal_entries AS strref => int
      BEGIN
        REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH
	 ~SetGlobal("A6StartARAM00","ARAM00",10)~
	 ~SetGlobal("A6StartARAM00","ARAM00",10)
	  EraseJournalEntry(%strref%)~
      END
    END
  END
END


// tof_spell_sphere
//
// This function takes a spell file (minus the .spl extention) and a
// list of sphere names, and informs Talents of Faerun about the spheres
// this spell belongs to.

DEFINE_ACTION_FUNCTION tof_spell_sphere
  STR_VAR
  spell		= ~dummy~
  sphere	= ~dummy~
BEGIN
<<<<<<<< .../stratagems-inline/%MOD_FOLDER%.ini
[spheres]
>>>>>>>>

  ACTION_IF NOT FILE_EXISTS "%data_loc_shared%/dw_talents_3p/%MOD_FOLDER%.ini"
  BEGIN
    MKDIR "%data_loc_shared%/dw_talents_3p"
    COPY ".../stratagems-inline/%MOD_FOLDER%.ini" "%data_loc_shared%/dw_talents_3p"
  END

  APPEND_OUTER "%data_loc_shared%/dw_talents_3p/%MOD_FOLDER%.ini" "%spell%=%sphere%" UNLESS "^%spell%"
END


