// =============================
// Test for unsupported versions
// =============================

PRINT "Running ALWAYS block..."

ACTION_IF GAME_IS "bg1 totsc tutu tutu_totsc"
BEGIN
  FAIL ~Sorry, this mod does not support this version of BG1.  The use of BGEE is highly recommended.~
END

ACTION_IF FILE_EXISTS "dlc/sod-dlc.zip" OR FILE_EXISTS "sod-dlc.zip"
BEGIN
  FAIL ~You need to run modmerge or install A7-DlcMerger before installing this mod.~
END


// ========================================
// Stuff that is done only once per install
// ========================================

ACTION_IF NOT VARIABLE_IS_SET initialise  
BEGIN
  PRINT "Running once-per-install block..."

  // Seed random number generator

  RANDOM_SEED "null"


  // Set variables

  OUTER_SET initialise = 1

  OUTER_TEXT_SPRINT ini_file "%MOD_FOLDER%.ini"

  OUTER_FOR (i = 1; i <= 10; ++i)
  BEGIN
    ACTION_IF GAME_IS "eet"
    BEGIN
      OUTER_SET EVAL "bg2_chapter_%i%" = i + 12
    END
    ELSE
    BEGIN
      OUTER_SET EVAL "bg2_chapter_%i%" = i
    END
  END

  
  // Set up ini file system

  INCLUDE ~%MOD_FOLDER%/lib/lib_ini.tpa~
  OUTER_SPRINT ini_to_read "%MOD_FOLDER%/%MOD_FOLDER%.ini"
  LAF SFO_read_ini_file RET_ARRAY SFO_reserved_ini_hash END


  // Install Detectable Spells

  INCLUDE "%MOD_FOLDER%/lib/ds.tph"
  LAUNCH_ACTION_FUNCTION detectable_spells END


  // Include and initialize libraries

  INCLUDE "%MOD_FOLDER%/lib/missing_ids.tpa"

  INCLUDE "%MOD_FOLDER%/lib/g3_bgt_cpmvars.tpa"
  INCLUDE "%MOD_FOLDER%/lib/g3_bgee_cpmvars.tpa"
  INCLUDE "%MOD_FOLDER%/lib/g3_eet_cpmvars.tpa"

  INCLUDE "%MOD_FOLDER%/sfo/install_sfo.tpa"

  INCLUDE "%MOD_FOLDER%/lib/cd_functions.tph"
  INCLUDE "%MOD_FOLDER%/lib/mh_functions.tph"


  // Gather some data structures for SFO

  LAUNCH_ACTION_MACRO read_in_saving_throws
  LAUNCH_ACTION_MACRO read_in_thac0
  LAUNCH_ACTION_MACRO read_in_hit_point_levels
END	// done_init


// ==================
// Handle EE charsets
// ==================

LAUNCH_ACTION_FUNCTION handle_charsets_immutably
  STR_VAR
  tra_base	= "language"
  RET
  scs_tra_loc
END


