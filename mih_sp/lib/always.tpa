// =============================
// Test for unsupported versions
// =============================

PRINT "Running ALWAYS block..."

ACTION_IF GAME_IS "bg1 totsc tutu tutu_totsc"
BEGIN
  FAIL ~Sorry, this mod does not support this version of BG1.  The use of BGEE is highly recommended.~
END

ACTION_IF FILE_EXISTS "dlc/sod-dlc.zip" OR FILE_EXISTS "sod-dlc.zip"
BEGIN
  FAIL ~You need to run modmerge or install A7-DlcMerger before installing this mod.~
END


// ========================================
// Stuff that is done only once per install
// ========================================

ACTION_IF NOT VARIABLE_IS_SET "initialise_%MOD_FOLDER%"
BEGIN
  PRINT "Running once-per-install block..."
  OUTER_SET "initialise_%MOD_FOLDER%" = 1
  RANDOM_SEED "null"


  // Load all libraries

  ACTION_BASH_FOR "%MOD_FOLDER%/lib" "lib_.*\.tp[ah]"
  BEGIN
      PRINT "Processing library %BASH_FOR_FILESPEC%..."
    INCLUDE "%BASH_FOR_FILESPEC%"
  END


  // Set up ini file system

  OUTER_SPRINT ini_to_read "%MOD_FOLDER%/%MOD_FOLDER%.ini"
  LAF SFO_read_ini_file RET_ARRAY SFO_reserved_ini_hash END


  // Set up Detectable Spells

  ACTION_IF FILE_EXISTS "%MOD_FOLDER%/lib/lib_ds.tph"
  BEGIN
    LAUNCH_ACTION_FUNCTION detectable_spells END
    
    ACTION_BASH_FOR "%MOD_FOLDER%/lib" "ds_.*\.tp[ah]"
    BEGIN
      PRINT "Processing DS file %BASH_FOR_FILESPEC%..."
      INCLUDE "%BASH_FOR_FILESPEC%"
    END
  END


  // Set up SFO library

  INCLUDE "%MOD_FOLDER%/sfo/install_sfo.tpa"
  OUTER_TEXT_SPRINT component_loc "lib"

  LAUNCH_ACTION_MACRO read_in_saving_throws
  LAUNCH_ACTION_MACRO read_in_thac0
  LAUNCH_ACTION_MACRO read_in_hit_point_levels

  // Set up SSL compiler

  OUTER_SPRINT ssllibrary "%MOD_FOLDER%/ssl/library"


  // Set up crossplatform variables

  ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/crossplatform.tpa"
  BEGIN
    INCLUDE "%MOD_FOLDER%/%component_loc%/crossplatform.tpa"
  END


  // Install any files that might be missing
  // (Mostly used for animations between BGEE/BG2EE/SOD)

  ACTION_BASH_FOR "%MOD_FOLDER%/%component_loc%/resource/_copy" ".*"
  BEGIN
    ACTION_IF NOT FILE_EXISTS_IN_GAME "%BASH_FOR_FILE%"
    BEGIN
      PRINT "Installing missing file %BASH_FOR_FILE%..."
      COPY_LARGE "%BASH_FOR_FILESPEC%" "override"
    END
  END
END	// done_init


// ==================
// Handle EE charsets
// ==================

LAUNCH_ACTION_FUNCTION handle_charsets_immutably
  STR_VAR
  tra_base	= "language"
  RET
  scs_tra_loc
END


// ===================
// Refresh spell codes
// ===================

OUTER_SET macro_spellcodes_read=0
LAUNCH_ACTION_MACRO read_in_spellcodes


