// Copy over files that don't need further editing

LAUNCH_ACTION_FUNCTION install
  INT_VAR
  overwrite	= 0
  STR_VAR
  file		= "all"
  location	= "resource/missing/_copy"
END


// Missing dir.ids on older games, creates dialog/script compile issues.

ACTION_IF NOT FILE_EXISTS_IN_GAME "dir.ids"
BEGIN
  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file	= "dir.ids"
    location	= "resource/missing"
  END

  LAUNCH_ACTION_FUNCTION swap_text
    STR_VAR
    file	= "action.ids"
    swaps	= ~"I:Face\*" => "I:Face*DIR"~
  END
END


// Creature files used that are missing in some game versions.

ACTION_IF GAME_IS "bgee sod"
BEGIN
  OUTER_TEXT_SPRINT game "BGEE"
END
ELSE
BEGIN
  OUTER_TEXT_SPRINT game "OTHER"
END

COPY - "%MOD_FOLDER%/%component_loc%/resource/missing/cre_strref.2da" "%workspace%"
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows

  FOR (SET i = 0; i < rows; ++i)
  BEGIN
    LAUNCH_PATCH_FUNCTION read_table_entry
      INT_VAR
      rownum	= i
      colnum	= 0
      RET
      creature	= value
    END

    LAUNCH_PATCH_FUNCTION read_table_entry
      INT_VAR
      rownum	= i
      STR_VAR
      column	= "%game%"
      RET
      strref	= value
    END

    INNER_ACTION
    BEGIN
      LAUNCH_ACTION_FUNCTION install_creature
        INT_VAR
        overwrite	= 0
        STR_VAR
        creature
        editstring	= "name1_string=>%strref% name2_string=>%strref%"
        location	= "resource/missing"
      END
    END
  END


