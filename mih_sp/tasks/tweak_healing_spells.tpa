// 3.5e Healing Spells tweak

ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_array
BEGIN
  "CLERIC_CURE_LIGHT_WOUNDS"	=> 0
  "CLERIC_CURE_MODERATE_WOUNDS"	=> 1
  "CLERIC_CURE_MEDIUM_WOUNDS"	=> 2
  "CLERIC_CURE_SERIOUS_WOUNDS"	=> 3
  "CLERIC_CURE_CRITICAL_WOUNDS"	=> 4
END


ACTION_PHP_EACH spell_array AS spell_name => tra_ref
BEGIN
  ACTION_IF IDS_OF_SYMBOL ("spell" "%spell_name%") >= 0
  BEGIN
    LAUNCH_ACTION_FUNCTION RES_NUM_OF_SPELL_NAME
      STR_VAR
      spell_name
      RET
      spell_num
      spell_res
    END

    PRINT "Found %spell_name% as %spell_res%"

    COPY_EXISTING "%spell_res%.spl" "override"
      SAY UNIDENTIFIED_DESC ( AT "tra_ref" )

      READ_LONG 0x0034 spell_level
      
      LAUNCH_PATCH_FUNCTION CD_EXTEND-O-MATIC
        INT_VAR
        step_size		= 1
        step_dur		= 0
        level_cap		= spell_level * 5
      END

      GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
      PHP_EACH ab_array AS int => ab_offset
      BEGIN
        READ_SHORT (ab_offset + 0x0010) casting_level

        GET_OFFSET_ARRAY2 fx_array ab_offset SPL_V10_HEAD_EFFECTS
        PHP_EACH fx_array AS int => fx_offset
	BEGIN
	  READ_SHORT (fx_offset + 0x0000) opcode

	  PATCH_IF opcode == 17
	  BEGIN
	    WRITE_LONG (fx_offset + 0x0004) casting_level
	    WRITE_LONG (fx_offset + 0x001c) spell_level
	    WRITE_LONG (fx_offset + 0x0020) 8
	  END
	END
      END
  END
END


ACTION_CLEAR_ARRAY spell_array


