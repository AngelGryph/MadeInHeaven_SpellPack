// Flame Blade tweak

OUTER_FOR (SET i = 1; i <= 10; ++i)
BEGIN
  COPY_EXISTING "fblade.itm" "override/mh#fbl%i%.itm"
    LAUNCH_PATCH_FUNCTION mh_make_magical
      INT_VAR
      enchantment	= 2
    END

    WRITE_BYTE 0x0031 0x5f	// Scimitar

    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 233	// Stat: Proficiency Modifier
      target		= 1	// Self
      timing		= 2	// While equipped
      parameter1	= 1
      parameter2	= 0x5f	// Scimitar
    END

    LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
      INT_VAR
      thac0_bonus	= 4
      dicenumber	= 0
      dicesize		= 0
      damage_bonus	= 0
      damage_type	= 0
      flag_strength	= 0
    END

    LAUNCH_PATCH_FUNCTION ALTER_EFFECT
      INT_VAR
      match_opcode	= 12	// HP: Damage
      parameter1	= i
      dicenumber	= 1
      dicesize		= 8
    END
END


COPY_EXISTING "sppr206.spl" "override"
  SAY UNIDENTIFIED_DESC @0

  GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
  PHP_EACH ab_array AS int => ab_offset
  BEGIN
    READ_SHORT (ab_offset + 0x0010) casting_level
    
    SET bonus = casting_level / 2
    PATCH_IF bonus < 1
    BEGIN
      SET bonus = 1
    END
    PATCH_IF bonus > 10
    BEGIN
      SET bonus = 10
    END

    GET_OFFSET_ARRAY2 fx_array ab_offset SPL_V10_HEAD_EFFECTS
    PHP_EACH fx_array AS int => fx_offset
    BEGIN
      READ_SHORT (fx_offset + 0x0000) opcode

      PATCH_IF opcode == 111
      BEGIN
        WRITE_ASCII (fx_offset + 0x0014) "mh#fbl%bonus%" #8
      END
    END
  END


