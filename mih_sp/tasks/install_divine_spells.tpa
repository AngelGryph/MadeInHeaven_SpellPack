// mh#prs11 - Striking

ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs11.spl" 1 1 CLERIC_STRIKING
  SAY NAME1 ~Striking~
  //SAY UNIDENTIFIED_DESC ~~

  LAUNCH_PATCH_FUNCTION mh_prevent_spell_effect_stacking END


// mh#prs12 - Faerie Fire

ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs12.spl" 1 1 CLERIC_FAERIE_FIRE
  SPRINT spell_res "%DEST_RES%"

  SAY NAME1 ~Faerie Fire~
  //SAY UNIDENTIFIED_DESC ~~

  LAUNCH_PATCH_FUNCTION mh_prevent_spell_effect_stacking END
    
  IF_EXISTING
    LAUNCH_PATCH_FUNCTION ALTER_EFFECT
      INT_VAR
      match_opcode	= 7	// Color: Set by Palette
      parameter1	= 73	// Christmas Green
    END

    LAUNCH_PATCH_FUNCTION ALTER_EFFECT
      INT_VAR
      match_opcode	= 9	// Color: Glow Pulse
      parameter1	= 1433287680
    END

    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode	= 0	// Stat: AC versus Damage Type
      opcode		= 47	// Cure: Invisibility
      timing		= 1	// Permanent
      duration		= 0
      parameter1	= 0
      parameter2	= 0
    END

    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode	= 0	// Stat: AC versus Damage Type
      opcode		= 101	// Protection: from Opcode
      parameter1	= 0
      parameter2	= 20
    END
  END


ACTION_IF FILE_EXISTS_IN_GAME "hidespl.2da"
BEGIN
  COPY_EXISTING "hidespl.2da" "override"
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows
    READ_2DA_ENTRIES_NOW __read_hidespl cols

    FOR (i = 0; i < rows; ++i)
    BEGIN
      READ_2DA_ENTRY_FORMER __read_hidespl i 0 hide_res

      PATCH_IF "%spell_res%" STRING_EQUAL_CASE "%hide_res%"
      BEGIN
        SET_2DA_ENTRY_LATER __write_hidespl i 1 0
        SET_2DA_ENTRY_LATER __write_hidespl i 2 0
        SET_2DA_ENTRY_LATER __write_hidespl i 3 0
      END
    END

    SET_2DA_ENTRIES_NOW __write_hidespl cols

    BUT_ONLY_IF_IT_CHANGES
END


// mh#prs22 - Seeking

ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs22.spl" 2 2 CLERIC_SEEKING
  SAY NAME1 ~Seeking~
  //SAY UNIDENTIFIED_DESC ~~

  LAUNCH_PATCH_FUNCTION mh_prevent_spell_effect_stacking END


// mh#prs41 - Sticks to Snakes

ACTION_IF NOT FILE_EXISTS_IN_GAME "snakegsu.cre"
BEGIN
  COPY "%MOD_FOLDER%/actors/snakegsu.cre" "override"
    SAY NAME1 ~Giant Snake~
    SAY NAME2 ~Giant Snake~
END


ACTION_IF NOT FILE_EXISTS_IN_GAME "snakegsu.eff"
BEGIN
  CREATE EFF "snakegsu"
    WRITE_LONG		0x0010 67	// Summon: Creature Summoning
    WRITE_LONG		0x001c 5
    WRITE_SHORT		0x0024 1
    WRITE_LONG		0x002c 100
    WRITE_ASCIIE	0x0030 "snakegsu" #8
    WRITE_ASCII		0x0070 "spmonsum" #8
END


ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs41.spl" 1 4 CLERIC_STICKS_TO_SNAKES
  SAY NAME1 ~Sticks to Snakes~
  //SAY UNIDENTIFIED_DESC ~~


// mh#prs42 - Heroism

ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs42.spl" 1 4 CLERIC_HEROISM
  SAY NAME1 ~Heroism~
  //SAY UNIDENTIFIED_DESC ~~

  LAUNCH_PATCH_FUNCTION mh_prevent_spell_effect_stacking END


// mh#prs61 - Superheroism

ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs61.spl" 1 6 CLERIC_SUPERHEROISM
  SAY NAME1 ~Superheroism~
  //SAY UNIDENTIFIED_DESC ~~

  LAUNCH_PATCH_FUNCTION mh_prevent_spell_effect_stacking END


// mh#prs62 - Celestial Protection

ADD_SPELL "%MOD_FOLDER%/spells/divine/mh#prs61.spl" 1 6 CLERIC_CELESTIAL_PROTECTION
  SAY NAME1 ~Celestial Protection~
  //SAY UNIDENTIFIED_DESC ~~

  LAUNCH_PATCH_FUNCTION cd_apply_batch
    STR_VAR
    array_name		= "cd_immunity_poison_arrays"
  END

  LAUNCH_PATCH_FUNCTION cd_apply_batch
    STR_VAR
    array_name		= "cd_immunity_disease_arrays"
  END

  LAUNCH_PATCH_FUNCTION cd_apply_batch
    STR_VAR
    array_name		= "cd_immunity_poison_resistance_arrays"
    all_or		= "all"
  END


