// Restore Cure Blindness & Deafness

COPY_EXISTING "sppr317.spl" "override/sppr316.spl"
  SAY NAME1 @0
  SAY UNIDENTIFIED_DESC @1

  WRITE_ASCII 0x003a "sppr316c"

  LAUNCH_PATCH_FUNCTION ALTER_SPELL_HEADER
    STR_VAR
    icon	= "sppr316b"
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 79	// Cure: Disease
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 139	// Text: Display String
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 240	// Graphics: Remove Special Effect Icon
    match_parameter2	= 7	// Disease
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 77	// Cure: Feeblemindedness
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 240	// Graphics: Remove Special Effect Icon
    match_parameter2	= 48	// Feebleminded
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 321	// Removal: Effects specified by Resource
  END


COPY_EXISTING "sppr317.spl" "override"
              "k0pa001.spl" "override"	// ???
              "rr#palcd.spl" "override"	// aTweaks
  SAY UNIDENTIFIED_DESC @2

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 75	// Cure: Blindness
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 240	// Graphics: Remove Special Effect Icon
    match_parameter2	= 8	// Blind
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 81	// Cure: Deafness
  END

  LAUNCH_PATCH_FUNCTION DELETE_EFFECT
    INT_VAR
    match_opcode	= 240	// Graphics: Remove Special Effect Icon
    match_parameter2	= 112	// Deaf
  END

  LAUNCH_PATCH_FUNCTION ADD_SPELL_EFFECT
    INT_VAR
    opcode		= 17	// HP: Current HP Modifier
    target		= 2	// Pre-Target
    timing		= 1	// Permanent
    parameter1		= 5
    power		= 3
    resist_dispel	= 3
  END

  IF_EXISTS


<<<<<<<< inline/append_cure_bd.2da
sppr316	mh#cure_bd
>>>>>>>>

COPY_EXISTING "speldesc.2da" "override"
  APPEND_FILE TEXT "inline/append_cure_bd.2da"
  REPLACE "mh#cure_bd" @3


COPY_EXISTING_REGEXP "^.+\.sto$" "override"
  SET new_offset = 0
  GET_OFFSET_ARRAY cures_array STO_V10_CURES
  PHP_EACH cures_array AS int => cures_offset
  BEGIN
    READ_ASCII cures_offset cure_res

    PATCH_IF "%cure_res%" STRING_EQUAL_CASE "sppr307"
    BEGIN
      SET new_offset = cures_offset + 0x0c
      READ_LONG (cures_offset + 0x0008) new_price
    END
  END

  PATCH_IF new_offset > 0
  BEGIN
    INSERT_BYTES new_offset 0x0c
    WRITE_ASCII new_offset "sppr316"
    WRITE_LONG (new_offset + 0x0008) new_price

    PATCH_IF LONG_AT (0x002c) >= new_offset
    BEGIN
      WRITE_LONG 0x002c (THIS + 0x0c)
    END

    PATCH_IF LONG_AT (0x0034) >= new_offset
    BEGIN
      WRITE_LONG 0x0034 (THIS + 0x0c)
    END

    PATCH_IF LONG_AT (0x004c) >= new_offset
    BEGIN
      WRITE_LONG 0x004c (THIS + 0x0c)
    END

    WRITE_LONG 0x0074 (THIS + 1)
  END

  BUT_ONLY_IF_IT_CHANGES


